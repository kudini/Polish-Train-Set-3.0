// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Habbins sprites */
spriteset(sset_habbins_purchase, "gfx/habbins.png")     { tmpl_sprite_purchase(4, 60) }
spriteset(sset_habbins1, "gfx/habbins.png")             { tmpl_sprite_train12(4, 20) }
spriteset(sset_habbins1_open_empty, "gfx/habbins.png")  { tmpl_sprite_train12(4, 80) }
spriteset(sset_habbins1_open_full, "gfx/habbins.png")   { tmpl_sprite_train12(4, 120) }
spriteset(sset_habbins2, "gfx/habbins.png")             { tmpl_sprite_train12(4, 160) }
spriteset(sset_habbins2_open_empty, "gfx/habbins.png")  { tmpl_sprite_train12(4, 200) }
spriteset(sset_habbins2_open_full, "gfx/habbins.png")   { tmpl_sprite_train12(4, 240) }
spriteset(sset_habbins1_cut, "gfx/habbins.png")         { tmpl_sprite_train12(4, 280) }
spriteset(sset_habbins2_cut, "gfx/habbins.png")         { tmpl_sprite_train12(4, 320) }
spritegroup sgroup_habbins1 {
    loaded:     [sset_habbins1];
    loading:    [sset_habbins1_open_empty, sset_habbins1_open_full];
}
spritegroup sgroup_habbins2 {
    loaded:     [sset_habbins2];
    loading:    [sset_habbins2_open_empty, sset_habbins2_open_full];
}

/* Habbins switches */
switch(FEAT_TRAINS, SELF, switch_habbins_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_habbins_1_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_habbins1_cut;
    return sgroup_habbins1;
}
switch(FEAT_TRAINS, SELF, switch_habbins_2_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_habbins2_cut;
    return sgroup_habbins2;
}
random_switch(FEAT_TRAINS, SELF, switch_habbins_graphics) {
    1: return switch_habbins_1_graphics;
    1: return switch_habbins_2_graphics;
}

/* Define the actual train */
item(FEAT_TRAINS, item_habbins) {
    property {
		/* common properties */
        name:                           string(STR_HABBINS_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1991, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_SPECIAL_COVERED_WAGON_DEFINITION
        loading_speed:                  10;
        cost_factor:                    PURCHASE_COST_HABBINS;
        running_cost_factor:            RUNNING_COST_CARGO_LARGE;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 56*CARGO_SPECIAL_COVERED_WAGON_MULTIPLIER;
        weight:                         24 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

    graphics {
        purchase:                   sset_habbins_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_habbins_graphics;
        articulated_part:           switch_habbins_articulated;
    }
}