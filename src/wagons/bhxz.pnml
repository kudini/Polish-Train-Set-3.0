// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* Bhxz sprites */
spriteset(sset_bhxz_purchase, "gfx/ringhoffer.png")     { tmpl_sprite_purchase(4, 60) }
spriteset(sset_bhxz, "gfx/ringhoffer.png")              { tmpl_sprite_train10(4, 20) }
spriteset(sset_bhxz_open, "gfx/ringhoffer.png")         { tmpl_sprite_train10(4, 80) }
spriteset(sset_bhxz_cut, "gfx/ringhoffer.png")          { tmpl_sprite_train10(4, 120) }
spritegroup sgroup_bhxz {
    loaded:     [sset_bhxz];
    loading:    [sset_bhxz_open];
}

/* Bhxz switches */
switch(FEAT_TRAINS, SELF, switch_bhxz_articulated, extra_callback_info1) {
    1: return item_dummy_2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_bhxz_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_bhxz_cut;
    return sgroup_bhxz;
}
switch (FEAT_TRAINS, SELF, switch_bhxz_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_BRAKE_AIR), string(STR_HEATING_STEAM_TEXT));
	return string(STR_BRAKE_AIR);
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#define SITTING_PLACES 51

/* Define the actual train */
item(FEAT_TRAINS, item_bhxz) {
    property {
        /* common properties */
        name:                           string(STR_BHXZ_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              PKP_INTRODUCTION_DATE;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  6;
        cost_factor:                    PURCHASE_COST_BHXZ;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS+1;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 SITTING_PLACES;
        weight:                         39 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     62;
        retire_early:                   16;
    }
}
    graphics {
        purchase:                   sset_bhxz_purchase;
        additional_text:            switch_bhxz_add_text;
        default:                    switch_bhxz_graphics;
        articulated_part:           switch_bhxz_articulated;
        purchase_cargo_capacity:    return SITTING_PLACES;
        cargo_capacity:             return SITTING_PLACES;
        cargo_age_period:           switch_normal_cargo_age_period;
        colour_mapping:             switch_lights_wagon_olive_2;
    }
}