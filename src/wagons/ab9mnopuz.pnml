// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* A/B9mnopuz sprites */
spriteset(sset_ab9mnopuz_purchase, "gfx/wagons/158a-159a.png")         { tmpl_sprite_purchase(4, 60) }
spriteset(sset_ab9mnopuz_c2, "gfx/wagons/158a-159a.png")               { tmpl_sprite_train12(4, 20) }
spriteset(sset_ab9mnopuz_c1, "gfx/wagons/158a-159a.png")               { tmpl_sprite_train12(4, 80) }
spriteset(sset_ab9mnopuz_c2_open, "gfx/wagons/158a-159a.png")          { tmpl_sprite_train12(4, 120) }
spriteset(sset_ab9mnopuz_c1_open, "gfx/wagons/158a-159a.png")          { tmpl_sprite_train12(4, 160) }
spriteset(sset_ab9mnopuz_c2_l, "gfx/wagons/158a-159a.png")             { tmpl_sprite_train12_l(4, 200) }
spriteset(sset_ab9mnopuz_c2_r, "gfx/wagons/158a-159a.png")             { tmpl_sprite_train12_r(4, 200) }
spriteset(sset_ab9mnopuz_c1_l, "gfx/wagons/158a-159a.png")             { tmpl_sprite_train12_l(4, 240) }
spriteset(sset_ab9mnopuz_c1_r, "gfx/wagons/158a-159a.png")             { tmpl_sprite_train12_r(4, 240) }
spriteset(sset_ab9mnopuz_c2_cut, "gfx/wagons/158a-159a.png")           { tmpl_sprite_train12(4, 280) }
spriteset(sset_ab9mnopuz_c1_cut, "gfx/wagons/158a-159a.png")           { tmpl_sprite_train12(4, 320) }
spriteset(sset_ab9mnopuz_c2_rear, "gfx/wagons/158a-159a.png")          { tmpl_sprite_train(32, 360) }
spriteset(sset_ab9mnopuz_c2_rear_open, "gfx/wagons/158a-159a.png")     { tmpl_sprite_train(32, 400) }
spriteset(sset_ab9mnopuz_c1_rear, "gfx/wagons/158a-159a.png")          { tmpl_sprite_train(32, 440) }
spriteset(sset_ab9mnopuz_c1_rear_open, "gfx/wagons/158a-159a.png")     { tmpl_sprite_train(32, 480) }
spritegroup sgroup_ab9mnopuz_c2 {
    loaded:     [sset_ab9mnopuz_c2];
    loading:    [sset_ab9mnopuz_c2_open];
}
spritegroup sgroup_ab9mnopuz_c1 {
    loaded:     [sset_ab9mnopuz_c1];
    loading:    [sset_ab9mnopuz_c1_open];
}
spritegroup sgroup_ab9mnopuz_c2_rear {
    loaded:     [sset_ab9mnopuz_c2_rear];
    loading:    [sset_ab9mnopuz_c2_rear_open];
}
spritegroup sgroup_ab9mnopuz_c1_rear {
    loaded:     [sset_ab9mnopuz_c1_rear];
    loading:    [sset_ab9mnopuz_c1_rear_open];
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#define SITTING_PLACES 72

/* A/B9mnopuz switches */
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_articulated, extra_callback_info1) {
    1: return item_ab9mnopuz;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_2_cut_front, other_veh_is_hidden(1)) {
    1: return sset_ab9mnopuz_c2_cut;
    return sgroup_ab9mnopuz_c2;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_2_extra_front,
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_ab9mnopuz_c2_l;
    1..2:   return sset_ab9mnopuz_c2_r;
    return switch_ab9mnopuz_2_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_2_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_ab9mnopuz_2_extra_front;  // viewport
    return sgroup_ab9mnopuz_c2;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_2_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_ab9mnopuz_c2_rear;  // viewport & no curves
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_2_graphics, position_in_vehid_chain % 2) {
    1: return switch_ab9mnopuz_2_rear;
    return switch_ab9mnopuz_2_front;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_1_cut_front, other_veh_is_hidden(1)) {
    1: return sset_ab9mnopuz_c1_cut;
    return sgroup_ab9mnopuz_c1;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_1_extra_front, 
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_ab9mnopuz_c1_l;
    1..2:   return sset_ab9mnopuz_c1_r;
    return switch_ab9mnopuz_1_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_1_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_ab9mnopuz_1_extra_front;  // viewport
    return sgroup_ab9mnopuz_c1;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_1_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_ab9mnopuz_c1_rear;  // viewport & no curves
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_1_graphics, position_in_vehid_chain % 2) {
    1: return switch_ab9mnopuz_1_rear;
    return switch_ab9mnopuz_1_front;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_graphics, cargo_subtype % 2) {
    1: return switch_ab9mnopuz_1_graphics;
    return switch_ab9mnopuz_2_graphics;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_length, position_in_vehid_chain % 2) {
    1: return 4;
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_ab9mnopuz_capacity, (position_in_vehid_chain%2)*2 + cargo_subtype%2) {
    0: return 72-1;
    1: return 54-1;
    return 1;  // hack for prevent "N/D"
}
switch (FEAT_TRAINS, SELF, switch_ab9mnopuz_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_4L, string(STR_BRAKE_AIR), string(STR_HEATING_ELECTRIC_TEXT), string(STR_AB9MNOPUZ_TEXT), string(STR_INCOME_1_CLASS));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_AB9MNOPUZ_TEXT), string(STR_INCOME_1_CLASS));
}

#define CARGO_CAPACITY_AB9MNOPUZ    int(SITTING_PLACES/2)

/* Define the actual train */
item(FEAT_TRAINS, item_ab9mnopuz) {
    property {
        /* common properties */
        name:                           string(STR_AB9MNOPUZ_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2010, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_AB9MNOPUZ;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS+2;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          230 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 CARGO_CAPACITY_AB9MNOPUZ;
        weight:                         50 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON, VINFO_HIGH_SPEED_WAGON);
    }

    graphics {
        purchase:                   sset_ab9mnopuz_purchase;
        additional_text:            switch_ab9mnopuz_add_text;
        default:                    switch_ab9mnopuz_graphics;
        cargo_subtype_text:         switch_new_subtype_text;
        articulated_part:           switch_ab9mnopuz_articulated;
        length:                     switch_ab9mnopuz_length;
        purchase_cargo_capacity:    return CARGO_CAPACITY_AB9MNOPUZ;
        cargo_capacity:             switch_ab9mnopuz_capacity;
        cargo_age_period:           switch_new_cargo_age_period;
        refit_cost:                 switch_new_refit_cost;
    }
}