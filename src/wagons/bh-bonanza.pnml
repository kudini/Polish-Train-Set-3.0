// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Bh "bonanza" spritess */
spriteset(sset_bh_bon_old_purchase, "gfx/wagons/120a.png")     { tmpl_sprite_purchase(4, 60) }
spriteset(sset_bh_bon_new_purchase, "gfx/wagons/120a.png")     { tmpl_sprite_purchase(4, 120) }
spriteset(sset_bh_bon_old, "gfx/wagons/120a.png")              { tmpl_sprite_train12(4, 20) }
spriteset(sset_bh_bon_new, "gfx/wagons/120a.png")              { tmpl_sprite_train12(4, 80) }
spriteset(sset_bh_bon_old_open, "gfx/wagons/120a.png")         { tmpl_sprite_train12(4, 140) }
spriteset(sset_bh_bon_new_open, "gfx/wagons/120a.png")         { tmpl_sprite_train12(4, 180) }
spriteset(sset_bh_bon_old_cut, "gfx/wagons/120a.png")          { tmpl_sprite_train12(4, 220) }
spriteset(sset_bh_bon_new_cut, "gfx/wagons/120a.png")          { tmpl_sprite_train12(4, 260) }
spritegroup sgroup_bh_bon_old {
    loaded:     [sset_bh_bon_old];
    loading:    [sset_bh_bon_old_open];
}
spritegroup sgroup_bh_bon_new {
    loaded:     [sset_bh_bon_new];
    loading:    [sset_bh_bon_new_open];
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#ifdef STANDING_PLACES
    #undef STANDING_PLACES
#endif
#define SITTING_PLACES 80-24
#define STANDING_PLACES 8

/* Bh "bonanza" switches */
#define IS_BH_BON_NEW_LIVERY    build_year >= 1993
#define IS_BH_BON_ENGINE_LIVERY build_year >= 2003
switch(FEAT_TRAINS, SELF, switch_bh_bon_purchase, IS_BH_BON_NEW_LIVERY) {
    1: return sset_bh_bon_new_purchase;
    return sset_bh_bon_old_purchase;
}
switch(FEAT_TRAINS, SELF, switch_bh_bon_articulated, extra_callback_info1) {
    1: return item_bh_mail_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_bh_bon_old_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_bh_bon_old_cut;
    return sgroup_bh_bon_old;
}
switch(FEAT_TRAINS, SELF, switch_bh_bon_new_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_bh_bon_new_cut;
    return sgroup_bh_bon_new;
}
switch(FEAT_TRAINS, PARENT, switch_bh_bon_engine_graphics, IS_BH_BON_ENGINE_LIVERY) {
    1: return switch_bh_bon_new_graphics;
    return switch_bh_bon_old_graphics;
}
switch(FEAT_TRAINS, SELF, switch_bh_bon_graphics, IS_BH_BON_NEW_LIVERY) {
    1: return switch_bh_bon_new_graphics;
    return switch_bh_bon_engine_graphics;
}
switch(FEAT_TRAINS, SELF, switch_bh_bon_capacity, cargo_type_in_veh) {
    TOUR: return SITTING_PLACES;
    return SITTING_PLACES+STANDING_PLACES;
}
switch (FEAT_TRAINS, SELF, switch_bh_bon_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_4L, string(STR_BRAKE_AIR), string(STR_HEATING_ST_EL_TEXT), string(STR_BH_BON_TEXT), string(STR_INCOME_LOWER));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_BH_BON_TEXT), string(STR_INCOME_LOWER));
}

/* Define the actual train */
item(FEAT_TRAINS, item_bh_bon) {
    property {
        /* common properties */
        name:                           string(STR_BH_BON_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1973, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  11; // 15;
        cost_factor:                    PURCHASE_COST_BH_BON;
        running_cost_factor:            RUNNING_COST_PASS_SUBURB;
        cargo_age_period:               int(185*0.84);  // faster ageing
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          140 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 SITTING_PLACES+STANDING_PLACES;
        weight:                         39 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   switch_bh_bon_purchase;
        additional_text:            switch_bh_bon_add_text;
        default:                    switch_bh_bon_graphics;
        articulated_part:           switch_bh_bon_articulated;
        purchase_cargo_capacity:    return SITTING_PLACES+STANDING_PLACES;
        cargo_capacity:             switch_bh_bon_capacity;
        colour_mapping:             switch_lights_wagon_green_2;
    }
}