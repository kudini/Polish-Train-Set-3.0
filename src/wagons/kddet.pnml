// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Kddet sprites */
spriteset(sset_kddet_purchase, "gfx/kddet.png")         { tmpl_sprite_purchase(4, 60) }
spriteset(sset_kddet, "gfx/kddet.png")                  { tmpl_sprite_train(4, 20) }
spriteset(sset_kddet_open, "gfx/kddet.png")             { tmpl_sprite_train(4, 80) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_kddet,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/kddet-x2.png")   { tmpl_sprite_train_x2(8, 40) }
alternative_sprites(sset_kddet_open,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/kddet-x2.png")   { tmpl_sprite_train_x2(8, 120) }
#endif
spritegroup sgroup_kddet {
    loaded:     [sset_kddet_open, sset_kddet, sset_kddet];
    loading:    [sset_kddet_open];
}

/* Define the actual train */
item(FEAT_TRAINS, item_kddet) {
    property {
        /* common properties */
        name:                           string(STR_KDDET_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1936, 1, 1);
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_NORMAL_COVERED_WAGON_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_KDDET;
        running_cost_factor:            RUNNING_COST_CARGO_SMALL;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          80 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 21*CARGO_NORMAL_COVERED_WAGON_MULTIPLIER;
        weight:                         12 ton;
        length:                         7;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     67;
        retire_early:                   16;
    }
}

    graphics {
        purchase:                   sset_kddet_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    sgroup_kddet;
    }
}