// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Fg sprites */
spriteset(sset_fg_purchase, "gfx/fg.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_fg, "gfx/fg.png")                { tmpl_sprite_train(4, 20) }
spriteset(sset_fg_open, "gfx/fg.png")           { tmpl_sprite_train(4, 80) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_fg,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/fg-x2.png")      { tmpl_sprite_train_x2(8, 40) }
alternative_sprites(sset_fg_open,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/fg-x2.png")      { tmpl_sprite_train_x2(8, 120) }
#endif
spritegroup sgroup_fg {
    loaded:     [sset_fg];
    loading:    [sset_fg_open];
}

/* Fg switches */
switch(FEAT_TRAINS, SELF, switch_fg_articulated, extra_callback_info1) {
    1: return item_goods_dummy_1;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/* Define goods special articulated part */
item(FEAT_TRAINS, item_goods_dummy_1) {
    property {
        /* common properties */
        climates_available:             NO_CLIMATE;
        introduction_date:              PKP_INTRODUCTION_DATE;
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        // only goods available
        refittable_cargo_classes:       bitmask();
        non_refittable_cargo_classes:   bitmask();
        cargo_allow_refit:              [GOOD];
        default_cargo_type:             GOOD;
        loading_speed:                  3;
        cost_factor:                    0;
        running_cost_factor:            0;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        cargo_capacity:                 14; // crates of goods
        weight:                         0;
        length:                         1;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                    sset_dummy;
    }
}

/* Define the actual train */
item(FEAT_TRAINS, item_fg) {
    property {
        /* common properties */
        name:                           string(STR_FG_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1925, 1, 1);
        vehicle_life:                   20;
        reliability_decay:              20;
        // only mail available
        refittable_cargo_classes:       bitmask();
        non_refittable_cargo_classes:   bitmask();
        cargo_allow_refit:              [MAIL];
        default_cargo_type:             MAIL;
        loading_speed:                  4;
        cost_factor:                    PURCHASE_COST_FG;
        running_cost_factor:            RUNNING_COST_PASS_OLD+1 + RUNNING_COST_POSTMAN;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          90 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 int(8*POST_MULTIPLIER); // bags of mail
        weight:                         19 ton;
        length:                         7;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     68;
        retire_early:                   16;
    }
}
    graphics {
        purchase:                   sset_fg_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    sgroup_fg;
        articulated_part:           switch_fg_articulated;
    }
}