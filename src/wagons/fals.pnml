// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Fals sprites */
spriteset(sset_fals_purchase, "gfx/wagons/wap.png")        { tmpl_sprite_purchase(4, 60) }
spriteset(sset_fals_gray_empty, "gfx/wagons/wap.png")      { tmpl_sprite_train(4, 20) }
spriteset(sset_fals_brown_empty, "gfx/wagons/wap.png")     { tmpl_sprite_train(4, 80) }
spriteset(sset_fals_gray_full, "gfx/wagons/wap.png")       { tmpl_sprite_train(4, 120) }
spriteset(sset_fals_brown_full, "gfx/wagons/wap.png")      { tmpl_sprite_train(4, 160) }
spritegroup sgroup_fals_gray {
    loaded:     [sset_fals_gray_empty, sset_fals_gray_full];
    loading:    [sset_fals_gray_empty, sset_fals_gray_full];
}
spritegroup sgroup_fals_brown {
    loaded:     [sset_fals_brown_empty, sset_fals_brown_full];
    loading:    [sset_fals_brown_empty, sset_fals_brown_full];
}

/* Fals switches */
random_switch(FEAT_TRAINS, SELF, switch_fals_graphics) {
    3: return sgroup_fals_gray;
    1: return sgroup_fals_brown;
}

/* Define the actual train */
item(FEAT_TRAINS, item_fals) {
    property {
        /* common properties */
        name:                           string(STR_FALS_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1973, 1, 1);
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_NORMAL_SELF_DISCHARGING_WAGON_DEFINITION
        loading_speed:                  15;
        cost_factor:                    PURCHASE_COST_FALS;
        running_cost_factor:            RUNNING_COST_CARGO_SPECIAL-1;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 44;
        weight:                         28 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     72;
        retire_early:                   16;
    }
}

    graphics {
        purchase:                   sset_fals_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_fals_graphics;
        colour_mapping:             switch_bulk_recolour;
    }
}