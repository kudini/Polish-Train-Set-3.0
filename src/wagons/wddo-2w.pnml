// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Wddo 2W sprites */
spriteset(sset_wddo_2w_purchase, "gfx/2w-gray.png")             { tmpl_sprite_purchase(4, 60) }
spriteset(sset_wddo_2w_gray_empty, "gfx/2w-gray.png")           { tmpl_sprite_train(4, 20) }
spriteset(sset_wddo_2w_gray_half, "gfx/2w-gray.png")            { tmpl_sprite_train(4, 80) }
spriteset(sset_wddo_2w_gray_full, "gfx/2w-gray.png")            { tmpl_sprite_train(4, 120) }
spriteset(sset_wddo_2w_gray_wood_half, "gfx/2w-gray.png")       { tmpl_sprite_train(4, 160) }
spriteset(sset_wddo_2w_gray_wood_full, "gfx/2w-gray.png")       { tmpl_sprite_train(4, 200) }
spriteset(sset_wddo_2w_gray_wdpr_half, "gfx/2w-gray.png")       { tmpl_sprite_train(4, 240) }
spriteset(sset_wddo_2w_gray_wdpr_full, "gfx/2w-gray.png")       { tmpl_sprite_train(4, 280) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_wddo_2w_gray_empty,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-gray-x2.png")     { tmpl_sprite_train_x2(8, 40) }
alternative_sprites(sset_wddo_2w_gray_half,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-gray-x2.png")     { tmpl_sprite_train_x2(8, 120) }
alternative_sprites(sset_wddo_2w_gray_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-gray-x2.png")     { tmpl_sprite_train_x2(8, 200) }
alternative_sprites(sset_wddo_2w_gray_wood_half,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-gray-x2.png")     { tmpl_sprite_train_x2(8, 280) }
alternative_sprites(sset_wddo_2w_gray_wood_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-gray-x2.png")     { tmpl_sprite_train_x2(8, 360) }
alternative_sprites(sset_wddo_2w_gray_wdpr_half,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-gray-x2.png")     { tmpl_sprite_train_x2(8, 440) }
alternative_sprites(sset_wddo_2w_gray_wdpr_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-gray-x2.png")     { tmpl_sprite_train_x2(8, 520) }
#endif
spritegroup sgroup_wddo_2w_gray {
    loaded:     [sset_wddo_2w_gray_empty, sset_wddo_2w_gray_half, sset_wddo_2w_gray_full];
    loading:    [sset_wddo_2w_gray_empty, sset_wddo_2w_gray_half, sset_wddo_2w_gray_full];
}
spritegroup sgroup_wddo_2w_gray_wood {
    loaded:     [sset_wddo_2w_gray_empty, sset_wddo_2w_gray_wood_half, sset_wddo_2w_gray_wood_full];
    loading:    [sset_wddo_2w_gray_empty, sset_wddo_2w_gray_wood_half, sset_wddo_2w_gray_wood_full];
}
spritegroup sgroup_wddo_2w_gray_wdpr {
    loaded:     [sset_wddo_2w_gray_empty, sset_wddo_2w_gray_wdpr_half, sset_wddo_2w_gray_wdpr_full];
    loading:    [sset_wddo_2w_gray_empty, sset_wddo_2w_gray_wdpr_half, sset_wddo_2w_gray_wdpr_full];
}
spriteset(sset_wddo_2w_brown_empty, "gfx/2w.png")             { tmpl_sprite_train(4, 20) }
spriteset(sset_wddo_2w_brown_half, "gfx/2w.png")              { tmpl_sprite_train(4, 80) }
spriteset(sset_wddo_2w_brown_full, "gfx/2w.png")              { tmpl_sprite_train(4, 120) }
spriteset(sset_wddo_2w_brown_wood_half, "gfx/2w.png")         { tmpl_sprite_train(4, 160) }
spriteset(sset_wddo_2w_brown_wood_full, "gfx/2w.png")         { tmpl_sprite_train(4, 200) }
spriteset(sset_wddo_2w_brown_wdpr_half, "gfx/2w.png")         { tmpl_sprite_train(4, 240) }
spriteset(sset_wddo_2w_brown_wdpr_full, "gfx/2w.png")         { tmpl_sprite_train(4, 280) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_wddo_2w_brown_empty,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-x2.png")      { tmpl_sprite_train_x2(8, 40) }
alternative_sprites(sset_wddo_2w_brown_half,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-x2.png")      { tmpl_sprite_train_x2(8, 120) }
alternative_sprites(sset_wddo_2w_brown_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-x2.png")      { tmpl_sprite_train_x2(8, 200) }
alternative_sprites(sset_wddo_2w_brown_wood_half,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-x2.png")      { tmpl_sprite_train_x2(8, 280) }
alternative_sprites(sset_wddo_2w_brown_wood_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-x2.png")      { tmpl_sprite_train_x2(8, 360) }
alternative_sprites(sset_wddo_2w_brown_wdpr_half,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-x2.png")      { tmpl_sprite_train_x2(8, 440) }
alternative_sprites(sset_wddo_2w_brown_wdpr_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/2w-x2.png")      { tmpl_sprite_train_x2(8, 520) }
#endif
spritegroup sgroup_wddo_2w_brown {
    loaded:     [sset_wddo_2w_brown_empty, sset_wddo_2w_brown_half, sset_wddo_2w_brown_full];
    loading:    [sset_wddo_2w_brown_empty, sset_wddo_2w_brown_half, sset_wddo_2w_brown_full];
}
spritegroup sgroup_wddo_2w_brown_wood {
    loaded:     [sset_wddo_2w_brown_empty, sset_wddo_2w_brown_wood_half, sset_wddo_2w_brown_wood_full];
    loading:    [sset_wddo_2w_brown_empty, sset_wddo_2w_brown_wood_half, sset_wddo_2w_brown_wood_full];
}
spritegroup sgroup_wddo_2w_brown_wdpr {
    loaded:     [sset_wddo_2w_brown_empty, sset_wddo_2w_brown_wdpr_half, sset_wddo_2w_brown_wdpr_full];
    loading:    [sset_wddo_2w_brown_empty, sset_wddo_2w_brown_wdpr_half, sset_wddo_2w_brown_wdpr_full];
}

/* Wddo 2W switches */
switch(FEAT_TRAINS, SELF, switch_wddo_2w_capacity, cargo_type_in_veh) {
    WOOD: return 20;
    WDPR: return 20;
    return CB_FAILED;
}
switch(FEAT_TRAINS, SELF, switch_wddo_2w_loading_speed, cargo_type_in_veh) {
    WOOD: return 3;
    WDPR: return 3;     // 7 ticks
    return CB_FAILED;   // 5 ticks
}
random_switch(FEAT_TRAINS, SELF, switch_wddo_2w) {
    3: return sgroup_wddo_2w_gray;
    1: return sgroup_wddo_2w_brown;
}
random_switch(FEAT_TRAINS, SELF, switch_wddo_2w_wood) {
    dependent: switch_wddo_2w;
    3: return sgroup_wddo_2w_gray_wood;
    1: return sgroup_wddo_2w_brown_wood;
}
random_switch(FEAT_TRAINS, SELF, switch_wddo_2w_wdpr) {
    dependent: switch_wddo_2w;
    3: return sgroup_wddo_2w_gray_wdpr;
    1: return sgroup_wddo_2w_brown_wdpr;
}

/* Define the wagon without the break */
item(FEAT_TRAINS, item_wddo_2w) {
    property {
		/* common properties */
        name:                           string(STR_WDDO_2W_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1942, 1, 1);
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_NORMAL_OPEN_WAGON_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_WDDO_2W;
        running_cost_factor:            RUNNING_COST_CARGO_SMALL;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          80 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 24;
        weight:                         10 ton;
        length:                         7;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
	}
} else {
    property {
        model_life:                     54;
		retire_early:                   16;
	}
}

    graphics {
        WOOD:                       switch_wddo_2w_wood;
        WDPR:                       switch_wddo_2w_wdpr;
        purchase:                   sset_wddo_2w_purchase;
		additional_text:            return string(STR_PURCHASE_DESCR_2L, string(STR_BRAKE_AIR), string(STR_CAPACITY_NON_FRIABLE));
        default:                    switch_wddo_2w;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_wddo_2w_capacity;
        loading_speed:              switch_wddo_2w_loading_speed;
        colour_mapping:             switch_bulk_recolour;
    }
}