// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Kd sprites */
spriteset(sset_kd_purchase, "gfx/kd.png")     { tmpl_sprite_purchase(4, 60) }
spriteset(sset_kd, "gfx/kd.png")              { tmpl_sprite_train(4, 20) }
spriteset(sset_kd_open, "gfx/kd.png")         { tmpl_sprite_train(4, 80) }
spritegroup sgroup_kd {
    loaded:     [sset_kd_open, sset_kd, sset_kd];
    loading:    [sset_kd_open];
}

/* Kd switches */
switch(FEAT_TRAINS, SELF, switch_kd_capacity, hasbit(cargo_classes, CC_LIQUID)) {
    1: return 9;
    return CB_FAILED;
}
switch(FEAT_TRAINS, SELF, switch_kd_loading_speed, hasbit(cargo_classes, CC_LIQUID)) {
    1: return 2;        // 5 ticks
    return CB_FAILED;   // 4 ticks
}

/* Define the wagon without the break */
item(FEAT_TRAINS, item_kd) {
    property {
        /* common properties */
        name:                           string(STR_KD_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              PKP_INTRODUCTION_DATE;
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_EARLY_COVERED_WAGON_DEFINITION
        loading_speed:                  3;
        cost_factor:                    PURCHASE_COST_KD;
        running_cost_factor:            RUNNING_COST_CARGO_EARLY;
        cargo_age_period:               int(185*1.19);  // slow ageing
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          45 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 12*CARGO_EARLY_COVERED_WAGON_MULTIPLIER;
        weight:                         9 ton;
        length:                         5;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

if(param_orig_intro_dates) {
    property {
        introduction_date:              date(1882, 1, 1);
    }
}

if(!param_keep_old_vehs) {
    if(param_orig_intro_dates) {
        property {
            model_life:                 45+36;
            retire_early:               16;
        }
    } else {
        property {
            model_life:                 45;
            retire_early:               16;
        }
    }
}

    graphics {
        purchase:                   sset_kd_purchase;
        additional_text:            return string(STR_PURCHASE_DESCR_2L, string(STR_BRAKE_NONE), string(STR_CAPACITY_LIQUID));
        default:                    sgroup_kd;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_kd_capacity;
        loading_speed:              switch_kd_loading_speed;
    }
}