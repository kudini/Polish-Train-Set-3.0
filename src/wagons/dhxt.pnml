// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Dhxt sprites */
spriteset(sset_dhxt_purchase, "gfx/1f.png")         { tmpl_sprite_purchase(4, 60) }
spriteset(sset_dhxt, "gfx/1f.png")                  { tmpl_sprite_train12(4, 20) }
spriteset(sset_dhxt_open, "gfx/1f.png")             { tmpl_sprite_train12(4, 80) }
spriteset(sset_dhxt_cut, "gfx/1f.png")              { tmpl_sprite_train12(4, 120) }
spritegroup sgroup_dhxt {
    loaded:     [sset_dhxt];
    loading:    [sset_dhxt_open];
}

/* Dhxt switches */
switch(FEAT_TRAINS, SELF, switch_dhxt_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_dhxt_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_dhxt_cut;
    return sgroup_dhxt;
}

/* Define the actual train */
item(FEAT_TRAINS, item_dhxt) {
    property {
        /* common properties */
        name:                           string(STR_DHXT_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1954, 1, 1);
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_LUGGAGE_WAGON_DEFINITION
        loading_speed:                  6;
        cost_factor:                    PURCHASE_COST_DHXT;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS-1 + RUNNING_COST_POSTMAN;
        cargo_age_period:               int(185*1.41);  // slow ageing
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 36;
        weight:                         41 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON, VINFO_LUGGAGE_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     66;
        retire_early:                   16;
    }
}
    graphics {
        purchase:                   sset_dhxt_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_dhxt_graphics;
        articulated_part:           switch_dhxt_articulated;
        colour_mapping:             switch_lights_wagon_olive_2;
    }
}