// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* A/Bdnu sprites */
#define ABDNU_OLD_FILE  "gfx/111a-112a.png"
#define ABDNU_NEW_FILE  "gfx/111a-112a.png"
#define ABDNU_IC_FILE   "gfx/140a-141a.png"
// old livery
spriteset(sset_abdnu_old_purchase, ABDNU_OLD_FILE)      { tmpl_sprite_purchase(4, 60) }
spriteset(sset_abdnu_c2_old, ABDNU_OLD_FILE)            { tmpl_sprite_train12(4, 20) }
spriteset(sset_abdnu_c12_old, ABDNU_OLD_FILE)           { tmpl_sprite_train12(4, 140) }
spriteset(sset_abdnu_c1_old, ABDNU_OLD_FILE)            { tmpl_sprite_train12(4, 180) }
spriteset(sset_abdnu_c2_old_open, ABDNU_OLD_FILE)       { tmpl_sprite_train12(4, 300) }
spriteset(sset_abdnu_c12_old_open, ABDNU_OLD_FILE)      { tmpl_sprite_train12(4, 380) }
spriteset(sset_abdnu_c1_old_open, ABDNU_OLD_FILE)       { tmpl_sprite_train12(4, 420) }
spriteset(sset_abdnu_c2_old_cut, ABDNU_OLD_FILE)        { tmpl_sprite_train12(4, 540) }
spriteset(sset_abdnu_c12_old_cut, ABDNU_OLD_FILE)       { tmpl_sprite_train12(4, 620) }
// new livery
spriteset(sset_abdnu_new_purchase, ABDNU_NEW_FILE)      { tmpl_sprite_purchase(4, 120) }
spriteset(sset_abdnu_c2_new, ABDNU_NEW_FILE)            { tmpl_sprite_train12(4, 80) }
spriteset(sset_abdnu_c12_new, ABDNU_NEW_FILE)           { tmpl_sprite_train12(4, 220) }
spriteset(sset_abdnu_c1_new, ABDNU_NEW_FILE)            { tmpl_sprite_train12(4, 260) }
spriteset(sset_abdnu_c2_new_open, ABDNU_NEW_FILE)       { tmpl_sprite_train12(4, 340) }
spriteset(sset_abdnu_c12_new_open, ABDNU_NEW_FILE)      { tmpl_sprite_train12(4, 460) }
spriteset(sset_abdnu_c1_new_open, ABDNU_NEW_FILE)       { tmpl_sprite_train12(4, 500) }
spriteset(sset_abdnu_c2_new_cut, ABDNU_NEW_FILE)        { tmpl_sprite_train12(4, 580) }
spriteset(sset_abdnu_c12_new_cut, ABDNU_NEW_FILE)       { tmpl_sprite_train12(4, 660) }
spriteset(sset_abdnu_c1_new_cut, ABDNU_NEW_FILE)        { tmpl_sprite_train12(4, 700) }
// alternate IC livery
spriteset(sset_abdnu_c2_ic, ABDNU_IC_FILE)              { tmpl_sprite_train12(4, 20) }
spriteset(sset_abdnu_c1_ic, ABDNU_IC_FILE)              { tmpl_sprite_train12(4, 80) }
spriteset(sset_abdnu_c2_ic_open, ABDNU_IC_FILE)         { tmpl_sprite_train12(4, 120) }
spriteset(sset_abdnu_c1_ic_open, ABDNU_IC_FILE)         { tmpl_sprite_train12(4, 160) }
spriteset(sset_abdnu_c2_ic_cut, ABDNU_IC_FILE)          { tmpl_sprite_train12(4, 200) }
spriteset(sset_abdnu_c1_ic_cut, ABDNU_IC_FILE)          { tmpl_sprite_train12(4, 240) }
// old livery
spritegroup sgroup_abdnu_c2_old {
    loaded:     [sset_abdnu_c2_old];
    loading:    [sset_abdnu_c2_old_open];
}
spritegroup sgroup_abdnu_c12_old {
    loaded:     [sset_abdnu_c12_old];
    loading:    [sset_abdnu_c12_old_open];
}
spritegroup sgroup_abdnu_c1_old {
    loaded:     [sset_abdnu_c1_old];
    loading:    [sset_abdnu_c1_old_open];
}
// new livery
spritegroup sgroup_abdnu_c2_new {
    loaded:     [sset_abdnu_c2_new];
    loading:    [sset_abdnu_c2_new_open];
}
spritegroup sgroup_abdnu_c12_new {
    loaded:     [sset_abdnu_c12_new];
    loading:    [sset_abdnu_c12_new_open];
}
spritegroup sgroup_abdnu_c1_new {
    loaded:     [sset_abdnu_c1_new];
    loading:    [sset_abdnu_c1_new_open];
}
// alternate IC livery
spritegroup sgroup_abdnu_c2_ic {
    loaded:     [sset_abdnu_c2_ic];
    loading:    [sset_abdnu_c2_ic_open];
}
spritegroup sgroup_abdnu_c1_ic {
    loaded:     [sset_abdnu_c1_ic];
    loading:    [sset_abdnu_c1_ic_open];
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#define SITTING_PLACES 80

/* A/Bdnu switches */
#define IS_ABDNU_NEW_LIVERY     build_year >= 1993
#define IS_ABDNU_ENGINE_LIVERY  build_year >= 2003
switch(FEAT_TRAINS, SELF, switch_abdnu_purchase, IS_ABDNU_NEW_LIVERY) {
    1: return sset_abdnu_new_purchase;
    return sset_abdnu_old_purchase;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c2_old_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c2_old_cut;
    return sgroup_abdnu_c2_old;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c12_old_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c12_old_cut;
    return sgroup_abdnu_c12_old;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c1_old_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c12_old_cut;
    return sgroup_abdnu_c1_old;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_old_graphics, cargo_subtype % 3) {
    1: return switch_abdnu_c12_old_graphics;
    2: return switch_abdnu_c1_old_graphics;
    return switch_abdnu_c2_old_graphics;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c2_new_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c2_new_cut;
    return sgroup_abdnu_c2_new;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c12_new_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c12_new_cut;
    return sgroup_abdnu_c12_new;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c1_new_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c1_new_cut;
    return sgroup_abdnu_c1_new;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_new_graphics, cargo_subtype % 3) {
    1: return switch_abdnu_c12_new_graphics;
    2: return switch_abdnu_c1_new_graphics;
    return switch_abdnu_c2_new_graphics;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c2_ic_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c2_ic_cut;
    return sgroup_abdnu_c2_ic;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_c1_ic_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_abdnu_c1_ic_cut;
    return sgroup_abdnu_c1_ic;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_ic_graphics, cargo_subtype % 3) {
    1: return switch_abdnu_c12_new_graphics;    // no graphics for 1/2 class
    2: return switch_abdnu_c1_ic_graphics;
    return switch_abdnu_c2_ic_graphics;
}
switch(FEAT_TRAINS, PARENT, switch_abdnu_engine_graphics, IS_ABDNU_ENGINE_LIVERY) {
    1: return switch_abdnu_new_graphics;
    return switch_abdnu_old_graphics;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_graphics2, IS_ABDNU_NEW_LIVERY) {
    1: return switch_abdnu_new_graphics;
    return switch_abdnu_engine_graphics;
}
switch(FEAT_TRAINS, PARENT, switch_abdnu_graphics, bitmask_consist_info & bitmask(VINFO_HIGH_SPEED_WAGON)) {
    bitmask(VINFO_HIGH_SPEED_WAGON): return switch_abdnu_ic_graphics;
    return switch_abdnu_graphics2;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_capacity, cargo_subtype % 3) {
    1: return 64;
    2: return 54;
    return SITTING_PLACES;
}
switch (FEAT_TRAINS, SELF, switch_abdnu_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_4L, string(STR_BRAKE_AIR), string(STR_HEATING_ST_EL_TEXT), string(STR_ABDNU_TEXT), string(STR_INCOME_1_CLASS));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_ABDNU_TEXT), string(STR_INCOME_1_CLASS));
}
switch(FEAT_TRAINS, SELF, switch_abdnu_new_lights, cargo_subtype % 3) {
    2: return switch_lights_wagon_red_2;
    return switch_lights_wagon_green_2;
}
switch(FEAT_TRAINS, SELF, switch_abdnu_lights, IS_ABDNU_NEW_LIVERY) {
    1: return switch_abdnu_new_lights;
    return switch_lights_wagon_olive_2;
}

/* Define the actual train */
item(FEAT_TRAINS, item_abdnu) {
    property {
        /* common properties */
        name:                           string(STR_ABDNU_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1968, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_ABDNU;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 SITTING_PLACES;
        weight:                         39 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   switch_abdnu_purchase;
        additional_text:            switch_abdnu_add_text;
        default:                    switch_abdnu_graphics;
        cargo_subtype_text:         switch_old_subtype_text;
        articulated_part:           switch_abdnu_articulated;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_abdnu_capacity;
        cargo_age_period:           switch_old_cargo_age_period;
        colour_mapping:             switch_abdnu_lights;
        refit_cost:                 switch_old_refit_cost;
    }
}