// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* BDwxzd sprites */
spriteset(sset_bdwxzd_purchase, "gfx/wagons/609a.png")         { tmpl_sprite_purchase(4, 60) }
spriteset(sset_bdwxzd, "gfx/wagons/609a.png")                  { tmpl_sprite_train12(4, 20) }
spriteset(sset_bdwxzd_ic, "gfx/wagons/609a.png")               { tmpl_sprite_train12(4, 80) }
spriteset(sset_bdwxzd_open, "gfx/wagons/609a.png")             { tmpl_sprite_train12(4, 120) }
spriteset(sset_bdwxzd_ic_open, "gfx/wagons/609a.png")          { tmpl_sprite_train12(4, 160) }
spriteset(sset_bdwxzd_cut, "gfx/wagons/609a.png")              { tmpl_sprite_train12(4, 200) }
spriteset(sset_bdwxzd_ic_cut, "gfx/wagons/609a.png")           { tmpl_sprite_train12(4, 240) }
spritegroup sgroup_bdwxzd {
    loaded:     [sset_bdwxzd];
    loading:    [sset_bdwxzd_open];
}
spritegroup sgroup_bdwxzd_ic {
    loaded:     [sset_bdwxzd_ic];
    loading:    [sset_bdwxzd_ic_open];
}

/* BDwxzd switches */
switch(FEAT_TRAINS, SELF, switch_bdwxzd_articulated, extra_callback_info1) {
    1: return item_goods_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_bdwxzd_graphics_classic, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_bdwxzd_cut;
    return sgroup_bdwxzd;
}
switch(FEAT_TRAINS, SELF, switch_bdwxzd_graphics_ic, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_bdwxzd_ic_cut;
    return sgroup_bdwxzd_ic;
}
switch(FEAT_TRAINS, PARENT, switch_bdwxzd_graphics, bitmask_consist_info & bitmask(VINFO_HIGH_SPEED_WAGON)) {
    bitmask(VINFO_HIGH_SPEED_WAGON): return switch_bdwxzd_graphics_ic;
    return switch_bdwxzd_graphics_classic;
}
switch (FEAT_TRAINS, SELF, switch_bdwxzd_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_HEATING_ST_EL_TEXT), string(STR_INCOME_LOWER));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_BH_BON_TEXT), string(STR_INCOME_LOWER));
}

/* Define goods special articulated part */
item(FEAT_TRAINS, item_goods_dummy_3) {
    property {
        /* common properties */
        climates_available:             NO_CLIMATE;
        introduction_date:              PKP_INTRODUCTION_DATE;
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        // only goods available
        refittable_cargo_classes:       bitmask();
        non_refittable_cargo_classes:   bitmask();
        cargo_allow_refit:              [GOOD];
        default_cargo_type:             GOOD;
        loading_speed:                  4;
        cost_factor:                    0;
        running_cost_factor:            0;
        cargo_age_period:               int(185*1.41);  // slow ageing
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        cargo_capacity:                 24; // crates of goods
        weight:                         0;
        length:                         3;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                    sset_dummy;
    }
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#define SITTING_PLACES 32

/* Define the actual train */
item(FEAT_TRAINS, item_bdwxzd) {
    property {
        /* common properties */
        name:                           string(STR_BDWXZD_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1981, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  6;
        cost_factor:                    PURCHASE_COST_BDWXZD;
        running_cost_factor:            RUNNING_COST_PASS_SUBURB+1 + RUNNING_COST_POSTMAN;
        cargo_age_period:               int(185*0.84);  // faster ageing
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 SITTING_PLACES;
        weight:                         43 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON, VINFO_LUGGAGE_WAGON);
    }

    graphics {
        purchase:                   sset_bdwxzd_purchase;
        additional_text:            switch_bdwxzd_add_text;
        default:                    switch_bdwxzd_graphics;
        articulated_part:           switch_bdwxzd_articulated;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             return CB_FAILED;
        colour_mapping:             switch_lights_wagon_brown_2;
    }
}