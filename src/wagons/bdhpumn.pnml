// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Bdhpumn sprites */
// default livery
spriteset(sset_bdhpumn_yb_purchase, "gfx/bdhpumn.png")  { tmpl_sprite_purchase(4, 60) }
spriteset(sset_bdhpumn_yb, "gfx/bdhpumn.png")           { tmpl_sprite_train12(4, 20) }
spriteset(sset_bdhpumn_yb_open, "gfx/bdhpumn.png")      { tmpl_sprite_train12(4, 180) }
spriteset(sset_bdhpumn_yb_cut, "gfx/bdhpumn.png")       { tmpl_sprite_train12(4, 420) }
spriteset(sset_bdhpumn_yb_rear, "gfx/bdhpumn.png")      { tmpl_sprite_train12_rear(4, 20) }
spriteset(sset_bdhpumn_yb_rear_open, "gfx/bdhpumn.png") { tmpl_sprite_train12_rear(4, 180) }
spriteset(sset_bdhpumn_yb_l, "gfx/bdhpumn.png")         { tmpl_sprite_train12_l(4, 300) }
spriteset(sset_bdhpumn_yb_r, "gfx/bdhpumn.png")         { tmpl_sprite_train12_r(4, 300) }
// classic "Przewozy regionalne" livery
spriteset(sset_bdhpumn_pr_purchase, "gfx/bdhpumn.png")  { tmpl_sprite_purchase(4, 120) }
spriteset(sset_bdhpumn_pr, "gfx/bdhpumn.png")           { tmpl_sprite_train12(4, 80) }
spriteset(sset_bdhpumn_pr_open, "gfx/bdhpumn.png")      { tmpl_sprite_train12(4, 220) }
spriteset(sset_bdhpumn_pr_cut, "gfx/bdhpumn.png")       { tmpl_sprite_train12(4, 460) }
spriteset(sset_bdhpumn_pr_rear, "gfx/bdhpumn.png")      { tmpl_sprite_train12_rear(4, 80) }
spriteset(sset_bdhpumn_pr_rear_open, "gfx/bdhpumn.png") { tmpl_sprite_train12_rear(4, 220) }
spriteset(sset_bdhpumn_pr_l, "gfx/bdhpumn.png")         { tmpl_sprite_train12_l(4, 340) }
spriteset(sset_bdhpumn_pr_r, "gfx/bdhpumn.png")         { tmpl_sprite_train12_r(4, 340) }
// Inter-REGIO livery
spriteset(sset_bdhpumn_ir, "gfx/bdhpumn.png")           { tmpl_sprite_train12(4, 140) }
spriteset(sset_bdhpumn_ir_open, "gfx/bdhpumn.png")      { tmpl_sprite_train12(4, 260) }
spriteset(sset_bdhpumn_ir_cut, "gfx/bdhpumn.png")       { tmpl_sprite_train12(4, 500) }
spriteset(sset_bdhpumn_ir_rear, "gfx/bdhpumn.png")      { tmpl_sprite_train12_rear(4, 140) }
spriteset(sset_bdhpumn_ir_rear_open, "gfx/bdhpumn.png") { tmpl_sprite_train12_rear(4, 260) }
spriteset(sset_bdhpumn_ir_l, "gfx/bdhpumn.png")         { tmpl_sprite_train12_l(4, 380) }
spriteset(sset_bdhpumn_ir_r, "gfx/bdhpumn.png")         { tmpl_sprite_train12_r(4, 380) }
// default livery
spritegroup sgroup_bdhpumn_yb {
    loaded:     [sset_bdhpumn_yb];
    loading:    [sset_bdhpumn_yb_open];
}
spritegroup sgroup_bdhpumn_yb_rear {
    loaded:     [sset_bdhpumn_yb_rear];
    loading:    [sset_bdhpumn_yb_rear_open];
}
// classic "Przewozy regionalne" livery
spritegroup sgroup_bdhpumn_pr {
    loaded:     [sset_bdhpumn_pr];
    loading:    [sset_bdhpumn_pr_open];
}
spritegroup sgroup_bdhpumn_pr_rear {
    loaded:     [sset_bdhpumn_pr_rear];
    loading:    [sset_bdhpumn_pr_rear_open];
}
// Inter-REGIO livery
spritegroup sgroup_bdhpumn_ir {
    loaded:     [sset_bdhpumn_ir];
    loading:    [sset_bdhpumn_ir_open];
}
spritegroup sgroup_bdhpumn_ir_rear {
    loaded:     [sset_bdhpumn_ir_rear];
    loading:    [sset_bdhpumn_ir_rear_open];
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#ifdef STANDING_PLACES
    #undef STANDING_PLACES
#endif
#define SITTING_PLACES 132
#define STANDING_PLACES 16

/* Bdhpumn switches */
switch(FEAT_TRAINS, SELF, switch_bdhpumn_purchase, PKP_PR_LIVERY_AVAILABLE) {
    1: return sset_bdhpumn_pr_purchase;
    return sset_bdhpumn_yb_purchase;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_articulated, extra_callback_info1) {
    1: return item_bdhpumn;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
// default livery
switch(FEAT_TRAINS, SELF, switch_bdhpumn_yb_cut_front, other_veh_is_hidden(1)) {
    1: return sset_bdhpumn_yb_cut;  // hidden vehicle
    return sgroup_bdhpumn_yb;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_yb_extra_front,
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_bdhpumn_yb_l;
    1..2:   return sset_bdhpumn_yb_r;
    return switch_bdhpumn_yb_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_yb_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_bdhpumn_yb_extra_front;    // viewport
    return sgroup_bdhpumn_yb;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_yb_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_bdhpumn_yb_rear;   // viewport & no curves & no Z-offset
    return sset_dummy;
}
// classic "Przewozy regionalne" livery
switch(FEAT_TRAINS, SELF, switch_bdhpumn_pr_cut_front, other_veh_is_hidden(1)) {
    1: return sset_bdhpumn_pr_cut;  // hidden vehicle
    return sgroup_bdhpumn_pr;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_pr_extra_front,
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_bdhpumn_pr_l;
    1..2:   return sset_bdhpumn_pr_r;
    return switch_bdhpumn_pr_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_pr_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_bdhpumn_pr_extra_front;    // viewport
    return sgroup_bdhpumn_pr;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_pr_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_bdhpumn_pr_rear;   // viewport & no curves & no Z-offset
    return sset_dummy;
}
// Inter-REGIO livery
switch(FEAT_TRAINS, SELF, switch_bdhpumn_ir_cut_front, other_veh_is_hidden(1)) {
    1: return sset_bdhpumn_ir_cut;  // hidden vehicle
    return sgroup_bdhpumn_ir;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_ir_extra_front,
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_bdhpumn_ir_l;
    1..2:   return sset_bdhpumn_ir_r;
    return switch_bdhpumn_ir_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_ir_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_bdhpumn_ir_extra_front;    // viewport
    return sgroup_bdhpumn_ir;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_ir_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_bdhpumn_ir_rear;   // viewport & no curves & no Z-offset
    return sset_dummy;
}
random_switch(FEAT_TRAINS, SELF, switch_bdhpumn_auto_front) {
    6: return switch_bdhpumn_pr_front;
    2: return switch_bdhpumn_ir_front;
}
random_switch(FEAT_TRAINS, FORWARD_SELF(1), switch_bdhpumn_auto_rear) {
    dependent: switch_bdhpumn_auto_front;
    6: return switch_bdhpumn_pr_rear;
    2: return switch_bdhpumn_ir_rear;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_front, PKP_PR_LIVERY_AVAILABLE) {
    1: return switch_bdhpumn_auto_front;
    return switch_bdhpumn_yb_front;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_rear, PKP_PR_LIVERY_AVAILABLE) {
    1: return switch_bdhpumn_auto_rear;
    return switch_bdhpumn_yb_rear;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_graphics, position_in_vehid_chain % 2) {
    1: return switch_bdhpumn_rear;
    return switch_bdhpumn_front;
}
// colour_mapping
random_switch(FEAT_TRAINS, SELF, switch_bdhpumn_lights_auto_front) {
    dependent: switch_bdhpumn_auto_front;
    6: return switch_lights_wagon_ltblue_2;
    2: return switch_lights_wagon_dkblue_2;
}
random_switch(FEAT_TRAINS, FORWARD_SELF(1), switch_bdhpumn_lights_auto_rear) {
    dependent: switch_bdhpumn_auto_rear;
    6: return switch_lights_wagon_ltblue_1;
    2: return switch_lights_wagon_dkblue_1;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_lights_front, PKP_PR_LIVERY_AVAILABLE) {
    1: return switch_bdhpumn_lights_auto_front;
    return switch_lights_wagon_ltblue_2;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_lights_rear, PKP_PR_LIVERY_AVAILABLE) {
    1: return switch_bdhpumn_lights_auto_rear;
    return switch_lights_wagon_ltblue_1;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_recolour, position_in_vehid_chain % 2) {
    1: return switch_bdhpumn_lights_rear;
    return switch_bdhpumn_lights_front;
}

// to do - cargo_subtype?

switch(FEAT_TRAINS, SELF, switch_bdhpumn_length, position_in_vehid_chain % 2) {
    1: return 4;
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_tour_capacity, position_in_vehid_chain % 2) {
    0: return SITTING_PLACES-1;
    return 1;  // hack for prevent "N/D"
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_pass_capacity, position_in_vehid_chain % 2) {
    0: return SITTING_PLACES+STANDING_PLACES-1;
    return 1;  // hack for prevent "N/D"
}
switch(FEAT_TRAINS, SELF, switch_bdhpumn_capacity, cargo_type_in_veh) {
    TOUR: return switch_bdhpumn_tour_capacity;
    return switch_bdhpumn_pass_capacity;
}
switch (FEAT_TRAINS, SELF, switch_bdhpumn_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_HEATING_ELECTRIC_TEXT), string(STR_INCOME_LOWER));
    return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_BH_BON_TEXT), string(STR_INCOME_LOWER));
}

#define CARGO_CAPACITY_BDHPUMN      int((SITTING_PLACES+STANDING_PLACES)/2)

/* Define the actual train */
item(FEAT_TRAINS, item_bdhpumn) {
    property {
        /* common properties */
        name:                           string(STR_BDHPUMN_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1989, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  30;
        cost_factor:                    PURCHASE_COST_BDHPUMN;
        running_cost_factor:            RUNNING_COST_PASS_SUBURB+1;
        cargo_age_period:               int(185*0.84);  // faster ageing
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 CARGO_CAPACITY_BDHPUMN;
        weight:                         42 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   switch_bdhpumn_purchase;
        additional_text:            switch_bdhpumn_add_text;
        default:                    switch_bdhpumn_graphics;
//        cargo_subtype_text:         switch_bdhpumn_subtype_text;
        articulated_part:           switch_bdhpumn_articulated;
        length:                     switch_bdhpumn_length;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_bdhpumn_capacity;
        colour_mapping:             switch_bdhpumn_recolour;
    }
}