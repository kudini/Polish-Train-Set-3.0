// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Fhx sprites */
spriteset(sset_fhx_purchase, "gfx/wagons/lilpop-b3.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_fhx, "gfx/wagons/lilpop-b3.png")                { tmpl_sprite_train10(4, 20) }
spriteset(sset_fhx_open, "gfx/wagons/lilpop-b3.png")           { tmpl_sprite_train10(4, 80) }
spriteset(sset_fhx_cut, "gfx/wagons/lilpop-b3.png")            { tmpl_sprite_train10(4, 120) }
spritegroup sgroup_fhx {
    loaded:     [sset_fhx];
    loading:    [sset_fhx_open];
}

/* Fhx switches */
switch(FEAT_TRAINS, SELF, switch_fhx_articulated, extra_callback_info1) {
    1: return item_dummy_2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_fhx_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_fhx_cut;
    return sgroup_fhx;
}

/* Define the actual train */
item(FEAT_TRAINS, item_fhx) {
    property {
        /* common properties */
        name:                           string(STR_FHX_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1932, 1, 1);
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_LUGGAGE_WAGON_DEFINITION
        loading_speed:                  6;
        cost_factor:                    PURCHASE_COST_FHX;
        running_cost_factor:            RUNNING_COST_PASS_SUBURB-1 + RUNNING_COST_POSTMAN;
        cargo_age_period:               185;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 34;
        weight:                         38 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON, VINFO_LUGGAGE_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     66;
        retire_early:                   16;
    }
}
    graphics {
        purchase:                   sset_fhx_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_fhx_graphics;
        articulated_part:           switch_fhx_articulated;
        colour_mapping:             switch_lights_wagon_olive_2;
    }
}