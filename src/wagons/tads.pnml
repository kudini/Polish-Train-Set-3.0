// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Tadns sprites */
spriteset(sset_tadns_purchase, "gfx/wagons/439v.png")      { tmpl_sprite_purchase(4, 60) }
spriteset(sset_tadns, "gfx/wagons/439v.png")               { tmpl_sprite_train12(4, 20) }
spriteset(sset_tadns_open, "gfx/wagons/439v.png")          { tmpl_sprite_train12(4, 80) }
spriteset(sset_tadns_full, "gfx/wagons/439v.png")          { tmpl_sprite_train12(4, 120) }
spriteset(sset_tadns_cut, "gfx/wagons/439v.png")           { tmpl_sprite_train12(4, 160) }
spriteset(sset_tadns_open_cut, "gfx/wagons/439v.png")      { tmpl_sprite_train12(4, 200) }
spritegroup sgroup_tadns {
    loaded:     [sset_tadns_open, sset_tadns, sset_tadns];
    loading:    [sset_tadns_open, sset_tadns_full];
}
spritegroup sgroup_tadns_cut {
    loaded:     [sset_tadns_open_cut, sset_tadns_cut, sset_tadns_cut];
    loading:    [sset_tadns_cut];
}

/* Tadns switches */
switch(FEAT_TRAINS, SELF, switch_tadns_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_tadns_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sgroup_tadns_cut;
    return sgroup_tadns;
}

/* Define the actual train */
item(FEAT_TRAINS, item_tads) {
    property {
        /* common properties */
        name:                           string(STR_TADNS_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1998, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_COVERED_SELF_DISCHARGING_WAGON_DEFINITION
        loading_speed:                  12;
        cost_factor:                    PURCHASE_COST_TADS;
        running_cost_factor:            RUNNING_COST_CARGO_SPECIAL;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 53;
        weight:                         27 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

    graphics {
        purchase:                   sset_tadns_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_tadns_graphics;
        articulated_part:           switch_tadns_articulated;
        colour_mapping:             switch_bulk_recolour;
    }
}