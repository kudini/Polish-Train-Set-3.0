// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* Pdn sprites */
spriteset(sset_pdn_old_purchase, "gfx/wagons/101c.png")    { tmpl_sprite_purchase(4, 60) }
spriteset(sset_pdn_new_purchase, "gfx/wagons/101c.png")    { tmpl_sprite_purchase(4, 120) }
spriteset(sset_pdn_old, "gfx/wagons/101c.png")             { tmpl_sprite_train12(4, 20) }
spriteset(sset_pdn_new, "gfx/wagons/101c.png")             { tmpl_sprite_train12(4, 80) }
spriteset(sset_pdn_old_open, "gfx/wagons/101c.png")        { tmpl_sprite_train12(4, 140) }
spriteset(sset_pdn_new_open, "gfx/wagons/101c.png")        { tmpl_sprite_train12(4, 180) }
spriteset(sset_pdn_old_cut, "gfx/wagons/101c.png")         { tmpl_sprite_train12(4, 220) }
spriteset(sset_pdn_new_cut, "gfx/wagons/101c.png")         { tmpl_sprite_train12(4, 260) }
spritegroup sgroup_pdn_old {
    loaded:     [sset_pdn_old];
    loading:    [sset_pdn_old_open];
}
spritegroup sgroup_pdn_new {
    loaded:     [sset_pdn_new];
    loading:    [sset_pdn_new_open];
}

/* Pdn switches */
#define IS_PDN_NEW_LIVERY       build_year >= 1993
switch(FEAT_TRAINS, SELF, switch_pdn_purchase, IS_PDN_NEW_LIVERY) {
    1: return sset_pdn_new_purchase;
    return sset_pdn_old_purchase;
}
switch(FEAT_TRAINS, SELF, switch_pdn_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_pdn_old_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_pdn_old_cut;
    return sgroup_pdn_old;
}
switch(FEAT_TRAINS, SELF, switch_pdn_new_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_pdn_new_cut;
    return sgroup_pdn_new;
}
switch(FEAT_TRAINS, SELF, switch_pdn_graphics, IS_PDN_NEW_LIVERY) {
    1: return switch_pdn_new_graphics;
    return switch_pdn_old_graphics;
}

/* Define the actual train */
item(FEAT_TRAINS, item_pdn) {
    property {
		/* common properties */
        name:                           string(STR_PDN_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1963, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_MAIL_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_PDN;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS + RUNNING_COST_POSTMAN;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 int(36*POST_MULTIPLIER);
        weight:                         37 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   switch_pdn_purchase;
		additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_pdn_graphics;
        articulated_part:           switch_pdn_articulated;
    }
}
