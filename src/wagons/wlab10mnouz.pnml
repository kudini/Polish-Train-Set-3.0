// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* WLAB10mnouz sprites */
spriteset(sset_wlab10mnouz_purchase, "gfx/wagons/305ad.png")   { tmpl_sprite_purchase(4, 60) }
spriteset(sset_wlab10mnouz, "gfx/wagons/305ad.png")            { tmpl_sprite_train12(4, 20) }
spriteset(sset_wlab10mnouz_open, "gfx/wagons/305ad.png")       { tmpl_sprite_train12(4, 80) }
spriteset(sset_wlab10mnouz_l, "gfx/wagons/305ad.png")          { tmpl_sprite_train12_l(4, 120) }
spriteset(sset_wlab10mnouz_r, "gfx/wagons/305ad.png")          { tmpl_sprite_train12_r(4, 120) }
spriteset(sset_wlab10mnouz_cut, "gfx/wagons/305ad.png")        { tmpl_sprite_train12(4, 160) }
spriteset(sset_wlab10mnouz_rear, "gfx/wagons/305ad.png")       { tmpl_sprite_train12_rear(4, 20) }
spriteset(sset_wlab10mnouz_rear_open, "gfx/wagons/305ad.png")  { tmpl_sprite_train12_rear(4, 80) }
spritegroup sgroup_wlab10mnouz {
    loaded:     [sset_wlab10mnouz];
    loading:    [sset_wlab10mnouz_open];
}
spritegroup sgroup_wlab10mnouz_rear {
    loaded:     [sset_wlab10mnouz_rear];
    loading:    [sset_wlab10mnouz_rear_open];
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#define SITTING_PLACES 30

/* WLAB10mnouz switches */
switch(FEAT_TRAINS, SELF, switch_wlab10mnouz_articulated, extra_callback_info1) {
    1: return item_wlab10mnouz;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_wlab10mnouz_cut_front, other_veh_is_hidden(1)) {
    1: return sset_wlab10mnouz_cut;
    return sgroup_wlab10mnouz;
}
switch(FEAT_TRAINS, SELF, switch_wlab10mnouz_extra_front,
   [STORE_TEMP(curv_info_prev_cur, 0), STORE_TEMP(curv_info_cur_next, 1),
    STORE_TEMP((LOAD_TEMP(0)<0 && LOAD_TEMP(1)<0)?0:LOAD_TEMP(1), 1),
    STORE_TEMP((LOAD_TEMP(0)>0 && LOAD_TEMP(1)>0)?0:LOAD_TEMP(1), 1),
    LOAD_TEMP(1)]) {
    -2..-1: return sset_wlab10mnouz_l;
    1..2:   return sset_wlab10mnouz_r;
    return switch_wlab10mnouz_cut_front;
}
switch(FEAT_TRAINS, SELF, switch_wlab10mnouz_front, (extra_callback_info1&0xFF)==0) {
    1: return switch_wlab10mnouz_extra_front;  // viewport
    return sgroup_wlab10mnouz;
}
switch(FEAT_TRAINS, SELF, switch_wlab10mnouz_rear, (extra_callback_info1&0xFF)==0 && curv_info_prev_cur==0 && other_veh_z_offset(-1)==0 ) {
    1: return sgroup_wlab10mnouz_rear;  // viewport & no curves
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_wlab10mnouz_graphics, position_in_vehid_chain % 2) {
    1: return switch_wlab10mnouz_rear;
    return switch_wlab10mnouz_front;
}
switch(FEAT_TRAINS, SELF, switch_wlab10mnouz_length, position_in_vehid_chain % 2) {
    1: return 4;
    return 8;
}
switch (FEAT_TRAINS, SELF, switch_wlab10mnouz_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_3L, string(STR_BRAKE_AIR), string(STR_HEATING_ELECTRIC_TEXT), string(STR_INCOME_HIGH));
    return string(STR_PURCHASE_DESCR_2L, string(STR_BRAKE_AIR), string(STR_INCOME_HIGH));
}

#define CARGO_CAPACITY_WLAB10MNOUZ      int(SITTING_PLACES/2)

/* Define the actual train */
item(FEAT_TRAINS, item_wlab10mnouz) {
    property {
        /* common properties */
        name:                           string(STR_WLAB10MNOUZ_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2004, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  6;
        cost_factor:                    PURCHASE_COST_WLAB10MNOUZ;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS+1 + RUNNING_COST_SLEEP_CREW;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          200 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 CARGO_CAPACITY_WLAB10MNOUZ;
        weight:                         55 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   sset_wlab10mnouz_purchase;
        additional_text:            switch_wlab10mnouz_add_text;
        default:                    switch_wlab10mnouz_graphics;
        articulated_part:           switch_wlab10mnouz_articulated;
        length:                     switch_wlab10mnouz_length;
        purchase_cargo_capacity:    return CARGO_CAPACITY_WLAB10MNOUZ;
        cargo_capacity:             return CARGO_CAPACITY_WLAB10MNOUZ;
        cargo_age_period:           switch_sleep_cargo_age_period;
        colour_mapping:             switch_lights_wagon_dkblue_2;
    }
}