// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Falns sprites */
spriteset(sset_falns_brown_purchase, "gfx/436v.png")    { tmpl_sprite_purchase(4, 60) }
spriteset(sset_falns_blue_purchase, "gfx/436v.png")     { tmpl_sprite_purchase(124, 60) }
spriteset(sset_falns_brown_empty, "gfx/436v.png")       { tmpl_sprite_train(4, 20) }
spriteset(sset_falns_blue_empty, "gfx/436v.png")        { tmpl_sprite_train(4, 80) }
spriteset(sset_falns_brown_full, "gfx/436v.png")        { tmpl_sprite_train(4, 120) }
spriteset(sset_falns_blue_full, "gfx/436v.png")         { tmpl_sprite_train(4, 160) }
spritegroup sgroup_falns_brown {
    loaded:     [sset_falns_brown_empty, sset_falns_brown_full];
    loading:    [sset_falns_brown_empty, sset_falns_brown_full];
}
spritegroup sgroup_falns_blue {
    loaded:     [sset_falns_blue_empty, sset_falns_blue_full];
    loading:    [sset_falns_blue_empty, sset_falns_blue_full];
}

/* Falns switches */
#define IS_FALNS_BLUE   build_year >= 2003
random_switch(FEAT_TRAINS, SELF, switch_falns_random) {
    1: return sgroup_falns_brown;
    3: return sgroup_falns_blue;
}
switch(FEAT_TRAINS, SELF, switch_falns_purchase, IS_FALNS_BLUE) {
    1: return sset_falns_blue_purchase;
    return sset_falns_brown_purchase;
}
switch(FEAT_TRAINS, SELF, switch_falns_graphics, IS_FALNS_BLUE) {
    1: return switch_falns_random;
    return sgroup_falns_brown;
}

/* Define the actual train */
item(FEAT_TRAINS, item_falns) {
    property {
		/* common properties */
        name:                           string(STR_FALNS_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1997, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_NORMAL_SELF_DISCHARGING_WAGON_DEFINITION
        loading_speed:                  18;
        cost_factor:                    PURCHASE_COST_FALNS;
        running_cost_factor:            RUNNING_COST_CARGO_SPECIAL;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 53;
        weight:                         26 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

    graphics {
        purchase:                   switch_falns_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_falns_graphics;
        colour_mapping:             switch_bulk_recolour;
    }
}
