// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Gags sprites */
spriteset(sset_gags_purchase, "gfx/wagons/401ka.png")      { tmpl_sprite_purchase(4, 60) }
spriteset(sset_gags, "gfx/wagons/401ka.png")               { tmpl_sprite_train10(4, 20) }
spriteset(sset_gags_open_empty, "gfx/wagons/401ka.png")    { tmpl_sprite_train10(4, 80) }
spriteset(sset_gags_open_full, "gfx/wagons/401ka.png")     { tmpl_sprite_train10(4, 120) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_gags,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/wagons/401ka-x2.png")   { tmpl_sprite_train10_x2(8, 40) }
alternative_sprites(sset_gags_open_empty,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/wagons/401ka-x2.png")   { tmpl_sprite_train10_x2(8, 120) }
alternative_sprites(sset_gags_open_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/wagons/401ka-x2.png")   { tmpl_sprite_train10_x2(8, 200) }
#endif
spritegroup sgroup_gags {
    loaded:     [sset_gags_open_empty, sset_gags, sset_gags];
    loading:    [sset_gags_open_empty, sset_gags_open_full];
}

/* Gags switches */
switch(FEAT_TRAINS, SELF, switch_gags_articulated, extra_callback_info1) {
    1: return item_dummy_1;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_gags_capacity, hasbit(cargo_classes, CC_PIECE_GOODS)) {
    1: return 30;
    return CB_FAILED;
}

/* Define the actual train */
item(FEAT_TRAINS, item_gags) {
    property {
		/* common properties */
        name:                           string(STR_GAGS_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1968, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_NORMAL_COVERED_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_GAGS;
        running_cost_factor:            RUNNING_COST_CARGO_MEDIUM+1;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 49*CARGO_NORMAL_COVERED_WAGON_MULTIPLIER;
        weight:                         23 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

    graphics {
        purchase:                   sset_gags_purchase;
        additional_text:            return string(STR_PURCHASE_DESCR_2L, string(STR_BRAKE_AIR), string(STR_CAPACITY_NON_FRIABLE));
        default:                    sgroup_gags;
        articulated_part:           switch_gags_articulated;
        cargo_capacity:             switch_gags_capacity;
    }
}
