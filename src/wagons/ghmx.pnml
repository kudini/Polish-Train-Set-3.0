// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* Ghmx sprites */
spriteset(sset_ghmx_purchase, "gfx/wagons/ghmx.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_ghmx, "gfx/wagons/ghmx.png")                { tmpl_sprite_train12(4, 20) }
spriteset(sset_ghmx_open, "gfx/wagons/ghmx.png")           { tmpl_sprite_train12(4, 80) }
spriteset(sset_ghmx_cut, "gfx/wagons/ghmx.png")            { tmpl_sprite_train12(4, 120) }
spritegroup sgroup_ghmx {
    loaded:     [sset_ghmx];
    loading:    [sset_ghmx_open];
}

/* Ghmx switches */
switch(FEAT_TRAINS, SELF, switch_ghmx_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ghmx_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_ghmx_cut;
    return sgroup_ghmx;
}

/* Define the actual train */
item(FEAT_TRAINS, item_ghmx) {
    property {
        /* common properties */
        name:                           string(STR_GHMX_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1929, 1, 1);
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_MAIL_WAGON_DEFINITION
        loading_speed:                  7;
        cost_factor:                    PURCHASE_COST_GHMX;
        running_cost_factor:            RUNNING_COST_PASS_EXPRESS-1 + RUNNING_COST_POSTMAN;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 int(32*POST_MULTIPLIER);
        weight:                         41 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     72;
        retire_early:                   16;
    }
}
    graphics {
        purchase:                   sset_ghmx_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    switch_ghmx_graphics;
        articulated_part:           switch_ghmx_articulated;
    }
}