// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Gbs sprites */
spriteset(sset_gbs_purchase, "gfx/208kf.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_gbs, "gfx/208kf.png")                { tmpl_sprite_train_sym(4, 20) }
spriteset(sset_gbs_open_empty, "gfx/208kf.png")     { tmpl_sprite_train_sym(4, 80) }
spriteset(sset_gbs_open_full, "gfx/208kf.png")      { tmpl_sprite_train_sym(4, 120) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_gbs,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/208kf-x2.png")   { tmpl_sprite_train_sym_x2(8, 40) }
alternative_sprites(sset_gbs_open_empty,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/208kf-x2.png")   { tmpl_sprite_train_sym_x2(8, 120) }
alternative_sprites(sset_gbs_open_full,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/208kf-x2.png")   { tmpl_sprite_train_sym_x2(8, 200) }
#endif
spritegroup sgroup_gbs {
    loaded:     [sset_gbs_open_empty, sset_gbs, sset_gbs];
    loading:    [sset_gbs_open_empty, sset_gbs_open_full];
}

/* Define the actual train */
item(FEAT_TRAINS, item_gbs) {
    property {
		/* common properties */
        name:                           string(STR_GBS_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1981, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   20;
        reliability_decay:              20;
        CARGO_NORMAL_COVERED_WAGON_DEFINITION
        loading_speed:                  6;
        cost_factor:                    PURCHASE_COST_GBS;
        running_cost_factor:            RUNNING_COST_CARGO_MEDIUM;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        power:                          0;
        running_cost_base:              RUNNING_COST_STEAM;
        cargo_capacity:                 25*CARGO_NORMAL_COVERED_WAGON_MULTIPLIER;
        weight:                         14 ton;
        length:                         8;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_CARGO_WAGON);
    }

    graphics {
        purchase:                   sset_gbs_purchase;
        additional_text:            return string(STR_BRAKE_AIR);
        default:                    sgroup_gbs;
    }
}