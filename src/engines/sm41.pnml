// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* SM41 sprites */
spriteset(sset_sm41_purchase, "gfx/sm41.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_sm41, "gfx/sm41.png")                { tmpl_sprite_train(4, 20) }
spriteset(sset_sm41_rev, "gfx/sm41.png")            { tmpl_sprite_train(4, 80) }

/* SM41 switches */
switch(FEAT_TRAINS, SELF, switch_sm41_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_NONE_TEXT), string(STR_ENGINE_PASSENGER_FREIGHT));
    return string(STR_ENGINE_PASSENGER_FREIGHT);
}
switch(FEAT_TRAINS, PARENT, switch_sm41_graphics, vehicle_is_reversed) {
    1: return sset_sm41_rev;
    return sset_sm41;
}
switch(FEAT_TRAINS, PARENT, switch_sm41_visual_effect_local, vehicle_is_reversed) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -1, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, DISABLE_WAGON_POWER);
}
ENGINE_RUNNING_COST_SWITCH(sm41, SM41)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sm41)
ENGINE_VISUAL_EFFECT_SWITCH(sm41)

/* Define the actual train */
item(FEAT_TRAINS, item_sm41) {
    property {
        /* common properties */
        name:                           string(STR_SM41_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1958, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              25;     // 20;
        CARGO_LOCOMOTIVES_DEFINITION 
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_SM41;
        running_cost_factor:            RUNNING_COST_SM41;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          80 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          441 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         59 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.314;
        air_drag_coefficient:           0.07;
        length:                         7;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:         0 ton;
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
	}
} else {
    property {
        model_life:                     64;
		retire_early:                   16;
	}
}

    graphics {
        purchase:                   sset_sm41_purchase;
        additional_text:            switch_sm41_add_text;
        default:                    switch_sm41_graphics;
        visual_effect_and_powered:  switch_sm41_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(sm41)
        ENGINE_POWER_CALLBACKS(sm41)
    }
}