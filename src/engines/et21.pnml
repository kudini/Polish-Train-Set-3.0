// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* ET21 sprites */
spriteset(sset_et21_purchase, "gfx/et21.png")           { tmpl_sprite_purchase(4, 60) }
spriteset(sset_et21, "gfx/et21.png")                    { tmpl_sprite_train10(4, 20) }
spriteset(sset_et21_purchase_yellow, "gfx/et21.png")    { tmpl_sprite_purchase(4, 120) }
spriteset(sset_et21_yellow, "gfx/et21.png")             { tmpl_sprite_train10(4, 80) }

/* ET21 switches */
#define IS_ET21_YELLOW  build_year >= 1989
switch(FEAT_TRAINS, SELF, switch_et21_purchase, IS_ET21_YELLOW) {
    1: return sset_et21_purchase_yellow;
    return sset_et21_purchase;
}
switch(FEAT_TRAINS, SELF, switch_et21_articulated, extra_callback_info1) {
    1: return item_et21;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_et21_length, position_in_vehid_chain % 2) {
    1: return 1;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_et21_graphics, (position_in_vehid_chain % 2)*2 + (IS_ET21_YELLOW)) {
    0: return sset_et21;
    1: return sset_et21_yellow;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_et21_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_et21_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_MID), string(STR_ENGINE_FREIGHT));
	return string(STR_ENGINE_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(et21, ET21)
DIESEL_ELECTRIC_2_ENGINE_POWER_SWITCH(et21, KW_TO_HP(1860))
ENGINE_VISUAL_EFFECT_SWITCH(et21)

/* Define the actual train */
item(FEAT_TRAINS, item_et21) {
    property {
		/* common properties */
        name:                           string(STR_ET21_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1957, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_ET21;
        running_cost_factor:            RUNNING_COST_ET21;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          1860 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         121 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.281;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     70;
        retire_early:                   12;
    }
}
    
    graphics {
        purchase:                   switch_et21_purchase;
        additional_text:            switch_et21_add_text;
        default:                    switch_et21_graphics;
        articulated_part:           switch_et21_articulated;
        length:                     switch_et21_length;
        visual_effect_and_powered:  switch_et21_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(et21)
        ENGINE_POWER_CALLBACKS(et21)
    }
}
