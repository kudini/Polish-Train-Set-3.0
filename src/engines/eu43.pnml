// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* EU43(Traxx) sprites */
spriteset(sset_eu43_purchase, "gfx/traxx.png")  { tmpl_sprite_purchase(4, 60) }
spriteset(sset_eu43_pkp_cargo, "gfx/traxx.png") { tmpl_sprite_train10(4, 20) }
spriteset(sset_eu43_pkp_pr, "gfx/e186.png")      { tmpl_sprite_train10(4, 78) }
spriteset(sset_eu43_lotos, "gfx/e186.png")      { tmpl_sprite_train10(4, 116) }

/* EU43(Traxx) switches */
switch (FEAT_TRAINS, SELF, switch_eu43_subtype_text2, cargo_subtype) {
    0: return string(STR_SUBTYPE_LIVERY_PKP_CARGO);
    1: return string(STR_SUBTYPE_LIVERY_PKP_PR);
    2: return string(STR_SUBTYPE_LIVERY_LOTOS);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_eu43_subtype_text, PKP_LIVERIES_AVAILABLE) {
    1: return switch_eu43_subtype_text2;
    return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, switch_eu43_livery_graphics, cargo_subtype % 3) {
    0: return sset_eu43_pkp_cargo;
    1: return sset_eu43_pkp_pr;
    2: return sset_eu43_lotos;
    return sset_eu43_pkp_cargo;
}
switch(FEAT_TRAINS, SELF, switch_eu43_graphics, position_in_vehid_chain % 2) {
    0: return switch_eu43_livery_graphics;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_eu43_articulated, extra_callback_info1) {
    1: return item_eu43;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_eu43_length, position_in_vehid_chain % 2) {
    1: return 2;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_eu43_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_eu43_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_PASSENGER_FREIGHT));
	return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(eu43, EU43)
ELECTRIC_HI_ENGINE_POWER_SWITCH(eu43, KW_TO_HP(5600))
ENGINE_VISUAL_EFFECT_SWITCH(eu43)

switch(FEAT_TRAINS, SELF, switch_eu43_can_attach, vehicle_type_id) {
    item_twindexx:   return string(STR_ATTACH_DISALLOW, string(STR_EU43_NAME));
    item_twindexxcc: return string(STR_ATTACH_DISALLOW, string(STR_EU43_NAME));
    return switch_normal_can_attach;
}

/* Define the actual train */
item(FEAT_TRAINS, item_eu43) {
    property {
		/* common properties */
        name:                           string(STR_EU43_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2008, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_EU43;
        running_cost_factor:            RUNNING_COST_EU43;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          140 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          5600 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         85 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.360;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_eu43_purchase;
        additional_text:            switch_eu43_add_text;
        default:                    switch_eu43_graphics;
        cargo_subtype_text:         switch_eu43_subtype_text;
        articulated_part:           switch_eu43_articulated;
        length:                     switch_eu43_length;
        visual_effect_and_powered:  switch_eu43_visual_effect;
        can_attach_wagon:           switch_eu43_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(eu43)
        ENGINE_POWER_CALLBACKS(eu43)
    }
}
