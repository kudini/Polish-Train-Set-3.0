// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Ty2 sprites */
spriteset(sset_ty2_purchase, "gfx/ty2.png")     { tmpl_sprite_purchase(4, 100) }
spriteset(sset_ty2_f1, "gfx/ty2.png")           { tmpl_sprite_train10(4, 20) }
spriteset(sset_ty2_f2, "gfx/ty2.png")           { tmpl_sprite_train10(4, 20) }
spriteset(sset_ty2_tender, "gfx/ty2.png")       { tmpl_sprite_train(4, 60) }
spriteset(sset_ty2_tender_rev, "gfx/ty2.png")   { tmpl_sprite_train(4, 120) }
spriteset(sset_ty2_rev_f1, "gfx/ty2.png")          { tmpl_sprite_train10_offset(4, 160, 2) }
spriteset(sset_ty2_rev_f2, "gfx/ty2.png")          { tmpl_sprite_train10_offset(4, 160, 2) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_ty2_f1,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/ty2-x2.png")   { tmpl_sprite_train10_x2(8, 40) }
alternative_sprites(sset_ty2_f2,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/ty2-x2.png")   { tmpl_sprite_train10_x2(8, 120) }
alternative_sprites(sset_ty2_tender,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/ty2-x2.png")   { tmpl_sprite_train_x2(8, 200) }
alternative_sprites(sset_ty2_tender_rev,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/ty2-x2.png")   { tmpl_sprite_train_x2(8, 280) }
alternative_sprites(sset_ty2_rev_f1,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/ty2-x2.png")   { tmpl_sprite_train10_offset_x2(8, 360, 2) }
alternative_sprites(sset_ty2_rev_f2,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/ty2-x2.png")   { tmpl_sprite_train10_offset_x2(8, 440, 2) }
#endif

/* Ty2 switches */
switch(FEAT_TRAINS, SELF, switch_ty2_articulated, extra_callback_info1) {
    1..2: return item_ty2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ty2_length, position_in_vehid_chain % 3) {
    1: return 1;
    return 7;
}
switch(FEAT_TRAINS, SELF, switch_ty2_graphics_normal_frames, motion_counter % 4) {
    0..1: return sset_ty2_f1;
    return sset_ty2_f2;
}
switch(FEAT_TRAINS, SELF, switch_ty2_graphics_normal, position_in_vehid_chain % 3) {
    0: return switch_ty2_graphics_normal_frames;
	2: return sset_ty2_tender;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_ty2_graphics_rev_frames, motion_counter % 4) {
    0..1: return sset_ty2_rev_f1;
    return sset_ty2_rev_f2;
}
switch(FEAT_TRAINS, SELF, switch_ty2_graphics_rev, position_in_vehid_chain % 3) {
	0: return sset_ty2_tender_rev;
    2: return switch_ty2_graphics_rev_frames;
    return sset_dummy;
}
switch(FEAT_TRAINS, PARENT, switch_ty2_graphics, vehicle_is_reversed) {
	1: return switch_ty2_graphics_rev;
    return switch_ty2_graphics_normal;
}
switch(FEAT_TRAINS, SELF, switch_ty2_visual_effect_normal, position_in_vehid_chain % 3) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_ty2_visual_effect_rev, position_in_vehid_chain % 3) {
	2: return visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, PARENT, switch_ty2_visual_effect_local, vehicle_is_reversed) {
    1: return switch_ty2_visual_effect_rev;
    return switch_ty2_visual_effect_normal;
}
switch (FEAT_TRAINS, SELF, switch_ty2_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_MID), string(STR_ENGINE_FREIGHT));
	return string(STR_ENGINE_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(ty2, TY2)
STEAM_ENGINE_POWER_SWITCH(ty2, KW_TO_HP(1201))
ENGINE_VISUAL_EFFECT_SWITCH(ty2)

/* Define the actual train */
item(FEAT_TRAINS, item_ty2) {
    property {
        /* common properties */
        name:                           string(STR_TY2_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1942, 1, 1);
        vehicle_life:                   35;     // 30;
        reliability_decay:              10;     // 20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_TY2;
        running_cost_factor:            RUNNING_COST_TY2;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          80 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1201 kW;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         147 ton;	// 86+61 t
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.114;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
	}
} else {
    property {
        model_life:                     68;
		retire_early:                   16;
	}
}

    graphics {
        purchase:                   sset_ty2_purchase;
        additional_text:            switch_ty2_add_text;
        default:                    switch_ty2_graphics;
        articulated_part:           switch_ty2_articulated;
        length:                     switch_ty2_length;
        visual_effect_and_powered:  switch_ty2_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(ty2)
        ENGINE_POWER_CALLBACKS(ty2)
    }
}
