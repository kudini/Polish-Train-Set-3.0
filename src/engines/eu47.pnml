// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* EU47(Traxx) sprites */
spriteset(sset_eu47_purchase, "gfx/engines/eu47.png")  { tmpl_sprite_purchase(4, 60) }
spriteset(sset_eu47, "gfx/engines/eu47.png")           { tmpl_sprite_train10(4, 20) }

/* EU47(Traxx) switches */
switch(FEAT_TRAINS, SELF, switch_eu47_articulated, extra_callback_info1) {
    1: return item_eu47;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_eu47_length, position_in_vehid_chain % 2) {
    1: return 2;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_eu47_graphics, position_in_vehid_chain % 2) {
    0: return sset_eu47;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_eu47_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_eu47_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_EXPRESS));
	return string(STR_ENGINE_EXPRESS);
}
ENGINE_RUNNING_COST_SWITCH(eu47, EU47)
ELECTRIC_HI_ENGINE_POWER_SWITCH(eu47, KW_TO_HP(5600))
ENGINE_VISUAL_EFFECT_SWITCH(eu47)

switch(FEAT_TRAINS, SELF, switch_eu47_start_stop, (count_veh_id(item_twindexx)/2 > 7) || (count_veh_id(item_twindexxcc)/2 > 1) ) {
    1: return string(STR_TRAIN_TOO_LONG, string(STR_EU47_NAME));
    return CB_RESULT_NO_TEXT;
}

/* Define the actual train */
item(FEAT_TRAINS, item_eu47) {
    property {
		/* common properties */
        name:                           string(STR_EU47_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2011, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_EU47;
        running_cost_factor:            RUNNING_COST_EU47;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          5600 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         85 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.360;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_eu47_purchase;
        additional_text:            switch_eu47_add_text;
        default:                    switch_eu47_graphics;
        articulated_part:           switch_eu47_articulated;
        length:                     switch_eu47_length;
        visual_effect_and_powered:  switch_eu47_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_eu47_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(eu47)
        ENGINE_POWER_CALLBACKS(eu47)
    }
}
