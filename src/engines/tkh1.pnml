// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* TKh1 sprites */
spriteset(sset_tkh1_purchase, "gfx/engines/tkh1.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_tkh1, "gfx/engines/tkh1.png")                { tmpl_sprite_train(4, 20) }
spriteset(sset_tkh1_rev, "gfx/engines/tkh1.png")            { tmpl_sprite_train(4, 80) }

/* TKh1 switches */
switch(FEAT_TRAINS, PARENT, switch_tkh1_graphics, vehicle_is_reversed) {
	1: return sset_tkh1_rev;
    return sset_tkh1;
}
switch(FEAT_TRAINS, PARENT, switch_tkh1_visual_effect_local, vehicle_is_reversed) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_STEAM, 1, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_tkh1_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_LOW), string(STR_ENGINE_PASSENGER_FREIGHT));
	return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(tkh1, TKH1)
STEAM_ENGINE_POWER_SWITCH(tkh1, KW_TO_HP(213))
ENGINE_VISUAL_EFFECT_SWITCH(tkh1)

/* Define the actual train */
item(FEAT_TRAINS, item_tkh1) {
    property {
		/* common properties */
        name:                           string(STR_TKH1_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              PKP_INTRODUCTION_DATE;
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   40;     // 20;
        reliability_decay:              12;     // 20;
        CARGO_LOCOMOTIVES_DEFINITION 
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_TKH1;
        running_cost_factor:            RUNNING_COST_TKH1;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          45 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          213 kW;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         29 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.212;
        air_drag_coefficient:           0.07;
        length:                         6;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:         0 ton;
        bitmask_vehicle_info:           0;
    }

if(param_orig_intro_dates) {
    property {
        introduction_date:              date(1882, 1, 1);
    }
}

if(!param_keep_old_vehs) {
    if(param_orig_intro_dates) {
        property {
            model_life:                 51+36;
            retire_early:               12;
        }
    } else {
        property {
            model_life:                 51;
            retire_early:               12;
        }
    }
}

    graphics {
        purchase:                   sset_tkh1_purchase;
        additional_text:            switch_tkh1_add_text;
        default:                    switch_tkh1_graphics;
        visual_effect_and_powered:  switch_tkh1_visual_effect;
		can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(tkh1)
        ENGINE_POWER_CALLBACKS(tkh1)
    }
}
