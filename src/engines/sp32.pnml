// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* SP32 sprites */
spriteset(sset_sp32_purchase, "gfx/engines/sp32.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_sp32, "gfx/engines/sp32.png")                { tmpl_sprite_train10(4, 20) }
spriteset(sset_sp32_rev, "gfx/engines/sp32.png")            { tmpl_sprite_train10(4, 80) }

/* SP32 switches */
switch(FEAT_TRAINS, SELF, switch_sp32_articulated, extra_callback_info1) {
    1: return item_sp32;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_sp32_length, position_in_vehid_chain % 2) {
    1: return 1;
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_sp32_graphics_normal, position_in_vehid_chain % 2) {
    0: return sset_sp32;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_sp32_graphics_rev, position_in_vehid_chain % 2) {
    0: return sset_sp32_rev;
    return sset_dummy;
}
switch(FEAT_TRAINS, PARENT, switch_sp32_graphics, vehicle_is_reversed) {
    1: return switch_sp32_graphics_rev;
    return switch_sp32_graphics_normal;
}
switch(FEAT_TRAINS, SELF, switch_sp32_visual_effect_normal, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_sp32_visual_effect_rev, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, -1, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, PARENT, switch_sp32_visual_effect_local, vehicle_is_reversed) {
    1: return switch_sp32_visual_effect_rev;
    return switch_sp32_visual_effect_normal;
}
switch(FEAT_TRAINS, SELF, switch_sp32_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_MID), string(STR_ENGINE_PASSENGER));
    return string(STR_ENGINE_PASSENGER);
}
ENGINE_RUNNING_COST_SWITCH(sp32, SP32)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sp32)
ENGINE_VISUAL_EFFECT_SWITCH(sp32)

/* Define the actual train */
item(FEAT_TRAINS, item_sp32) {
    property {
        /* common properties */
        name:                           string(STR_SP32_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1985, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              25;     // 20;
        CARGO_LOCOMOTIVES_DEFINITION 
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_SP32;
        running_cost_factor:            RUNNING_COST_SP32;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1015 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         74 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.187;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:         0 ton;
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_sp32_purchase;
        additional_text:            switch_sp32_add_text;
        default:                    switch_sp32_graphics;
        articulated_part:           switch_sp32_articulated;
        length:                     switch_sp32_length;
        visual_effect_and_powered:  switch_sp32_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(sp32)
        ENGINE_POWER_CALLBACKS(sp32)
    }
}