// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* SA106 sprites */
spriteset(sset_sa106_purchase, "gfx/sa106.png")         { tmpl_sprite_purchase(4, 60) }
spriteset(sset_sa106, "gfx/sa106.png")                  { tmpl_sprite_train12(4, 20) }
spriteset(sset_sa106_open, "gfx/sa106.png")             { tmpl_sprite_train12(4, 80) }
spriteset(sset_sa106_cut, "gfx/sa106.png")              { tmpl_sprite_train12(4, 120) }
spritegroup sgroup_sa106 {
    loaded:     [sset_sa106];
    loading:    [sset_sa106_open];
}

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#ifdef STANDING_PLACES
    #undef STANDING_PLACES
#endif
#define SITTING_PLACES 60
#define STANDING_PLACES 16

/* SA106 switches */
switch(FEAT_TRAINS, SELF, switch_sa106_articulated, extra_callback_info1) {
    1: return item_dummy_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_sa106_graphics, (extra_callback_info1&0xFF)==0 && other_veh_is_hidden(1)) {
    1: return sset_sa106_cut;
    return sgroup_sa106;
}
switch(FEAT_TRAINS, SELF, switch_sa106_capacity, cargo_type_in_veh) {
    TOUR: return SITTING_PLACES;
    return SITTING_PLACES+STANDING_PLACES;
}
switch(FEAT_TRAINS, PARENT, switch_sa106_visual_effect_local,0) {
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_sa106_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_COMBINED_TEXT), string(STR_SA106_TEXT));
    return string(STR_SA106_TEXT);
}
switch(FEAT_TRAINS, SELF, switch_sa106_can_attach_arctic, IS_HEATING_CLIMATE ) {
    1: return string(STR_ATTACH_DISALLOW, string(STR_SA106_NAME));
    return CB_RESULT_ATTACH_ALLOW;
}
switch(FEAT_TRAINS, SELF, switch_sa106_can_attach, vehicle_type_id) {
    item_sa106:   return CB_RESULT_ATTACH_ALLOW;
    item_bh_bon:  return CB_RESULT_ATTACH_ALLOW;
    item_bdhpumn: return switch_sa106_can_attach_arctic;
    item_bdwxzd:  return switch_sa106_can_attach_arctic;
    return string(STR_ATTACH_DISALLOW, string(STR_SA106_NAME));
}
switch(FEAT_TRAINS, SELF, switch_sa106_start_stop, (count_veh_id(item_sa106) > 3) || (count_veh_id(item_bh_bon)+count_veh_id(item_bdhpumn)/2+count_veh_id(item_bdwxzd) > 3) ) {
    1: return string(STR_TRAIN_TOO_LONG, string(STR_SA106_NAME));
    return CB_RESULT_NO_TEXT;
}
ENGINE_RUNNING_COST_SWITCH(sa106, SA106)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sa106)
ENGINE_VISUAL_EFFECT_SWITCH(sa106)

/* Define the actual train */
item(FEAT_TRAINS, item_sa106) {
    property {
        /* common properties */
        name:                           string(STR_SA106_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2001, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  12;
        cost_factor:                    PURCHASE_COST_SA106;
        running_cost_factor:            RUNNING_COST_SA106;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          140 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          500 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        cargo_capacity:                 SITTING_PLACES+STANDING_PLACES;
        weight:                         52 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.150;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }
    graphics {
        purchase:                   sset_sa106_purchase;
        additional_text:            switch_sa106_add_text;
        default:                    switch_sa106_graphics;
        articulated_part:           switch_sa106_articulated;
        visual_effect_and_powered:  switch_sa106_visual_effect;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_sa106_capacity;
        colour_mapping:             switch_lights_engine_2;
        can_attach_wagon:           switch_sa106_can_attach;
        start_stop:                 switch_sa106_start_stop;
        ENGINE_RUNNING_COST_CALLBACKS(sa106)
        ENGINE_POWER_CALLBACKS(sa106)
    }
}