// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* EP02 sprites */
spriteset(sset_ep02_purchase, "gfx/engines/ep02.png")   { tmpl_sprite_purchase(4, 60) }
spriteset(sset_ep02, "gfx/engines/ep02.png")            { tmpl_sprite_train10(4, 20) }

/* EP02 switches */
switch(FEAT_TRAINS, SELF, switch_ep02_articulated, extra_callback_info1) {
    1: return item_ep02;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ep02_length, position_in_vehid_chain % 2) {
    1: return 1;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_ep02_graphics, position_in_vehid_chain % 2) {
    0: return sset_ep02;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_ep02_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_ep02_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_LOW), string(STR_ENGINE_EXPRESS));
    return string(STR_ENGINE_EXPRESS);
}
ENGINE_RUNNING_COST_SWITCH(ep02, EP02)
DIESEL_ELECTRIC_2_ENGINE_POWER_SWITCH(ep02, KW_TO_HP(1360))
ENGINE_VISUAL_EFFECT_SWITCH(ep02)

/* Define the actual train */
item(FEAT_TRAINS, item_ep02) {
    property {
        /* common properties */
        name:                           string(STR_EP02_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1953, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              25;     // 20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_EP02;
        running_cost_factor:            RUNNING_COST_EP02;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          110 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1360 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         81 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.213;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     29;
        retire_early:                   2;
    }
}

    graphics {
        purchase:                   sset_ep02_purchase;
        additional_text:            switch_ep02_add_text;
        default:                    switch_ep02_graphics;
        articulated_part:           switch_ep02_articulated;
        length:                     switch_ep02_length;
        visual_effect_and_powered:  switch_ep02_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(ep02)
        ENGINE_POWER_CALLBACKS(ep02)
    }
}
