// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* EN63A sprites */
spriteset(sset_en63a_purchase, "gfx/en63a-impuls.png")   { tmpl_sprite_purchase(4, 140) }
spriteset(sset_en63a_advert, "gfx/en63a-impuls.png")   { tmpl_sprite_advertisement200(4, 280) }

spriteset(sset_en63a_head, "gfx/en63a-impuls.png")       { tmpl_sprite_train12(4, 20) }
spriteset(sset_en63a_mid, "gfx/en63a-impuls.png")       { tmpl_sprite_train10_offset(4, 60, 1) }
spriteset(sset_en63a_tail, "gfx/en63a-impuls.png")       { tmpl_sprite_train12_offset(4, 100, 3) }
spriteset(sset_en63a_head_open, "gfx/en63a-impuls.png")  { tmpl_sprite_train12(4, 160) }
spriteset(sset_en63a_mid_open, "gfx/en63a-impuls.png")  { tmpl_sprite_train10_offset(4, 200, 1) }
spriteset(sset_en63a_tail_open, "gfx/en63a-impuls.png")  { tmpl_sprite_train12_offset(4, 240, 3) }
spritegroup sgroup_en63a_head {
    loaded:     [sset_en63a_head];
    loading:    [sset_en63a_head_open];
}
spritegroup sgroup_en63a_mid {
    loaded:     [sset_en63a_mid];
    loading:    [sset_en63a_mid_open];
}
spritegroup sgroup_en63a_tail {
    loaded:     [sset_en63a_tail];
    loading:    [sset_en63a_tail_open];
}

#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_en63a_head,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/en63a-impuls-x2.png")   { tmpl_sprite_train12_x2(8, 40) }
alternative_sprites(sset_en63a_mid,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/en63a-impuls-x2.png")   { tmpl_sprite_train10_offset_x2(8, 120, 1) }
alternative_sprites(sset_en63a_tail,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/en63a-impuls-x2.png")   { tmpl_sprite_train12_offset_x2(8, 200, 3) }
alternative_sprites(sset_en63a_head_open,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/en63a-impuls-x2.png")   { tmpl_sprite_train12_x2(8, 280) }
alternative_sprites(sset_en63a_mid_open,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/en63a-impuls-x2.png")   { tmpl_sprite_train10_offset_x2(8, 360, 1) }
alternative_sprites(sset_en63a_tail_open,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/en63a-impuls-x2.png")   { tmpl_sprite_train12_offset_x2(8, 440, 3) }
#endif

/* EN63A switches */
switch(FEAT_TRAINS, SELF, switch_en63a_articulated, extra_callback_info1) {
    1..4: return item_en63a;     // including dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_en63a_length, position_in_vehid_chain % 5) {
    1: return 4;    // dummy part
    3: return 3;    // dummy part
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_en63a_purchase, extra_callback_info1 & 0xFF) {
    0x21: return sset_en63a_advert;
    return sset_en63a_purchase;
}
switch(FEAT_TRAINS, SELF, switch_en63a_graphics, position_in_vehid_chain % 5) {
    0: return sgroup_en63a_head;
    2: return sgroup_en63a_mid;
    4: return sgroup_en63a_tail;
    return sset_dummy;      // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_en63a_tour_capacity, position_in_vehid_chain % 5) {
    0: return 52;
    2: return 52-2;
    4: return 52;
    return 1;   // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_en63a_pass_capacity, position_in_vehid_chain % 5) {
    0: return 52+26;    //52;
    2: return 52+26-2;  //52;
    4: return 52+26;    //52;
    return 1;   // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_en63a_capacity, cargo_type_in_veh) {
    TOUR: return switch_en63a_tour_capacity;
    return switch_en63a_pass_capacity;
}
switch(FEAT_TRAINS, SELF, switch_en63a_visual_effect_local, position_in_vehid_chain % 5) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 3, DISABLE_WAGON_POWER);
    3: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, -3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_en63a_can_attach, vehicle_type_id) {
    item_en57:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_en71:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed72:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed74:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_en76:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed250:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_en63a:  return CB_RESULT_ATTACH_ALLOW;
    item_ed161:  return string(STR_ATTACH_DISALLOW_EZT2);
    return string(STR_ATTACH_DISALLOW_EZT1);
}
switch (FEAT_TRAINS, SELF, switch_en63a_start_stop, num_vehs_in_consist / 5) {
    1..3: return CB_RESULT_NO_TEXT;
    return string(STR_TRAIN_TOO_LONG, string(STR_EN63A_NAME));
}
ENGINE_RUNNING_COST_SWITCH(en63a, EN63A)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(en63a)
ENGINE_VISUAL_EFFECT_SWITCH(en63a)

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#ifdef STANDING_PLACES
    #undef STANDING_PLACES
#endif
#define SITTING_PLACES 52*3
#define STANDING_PLACES 26*3
#define CARGO_CAPACITY_EN63A     int((SITTING_PLACES+STANDING_PLACES+2)/5)

/* Define the actual train */
item(FEAT_TRAINS, item_en63a) {
    property {
        /* common properties */
        name:                           string(STR_EN63A_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2014, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  11;     // per part
        cost_factor:                    PURCHASE_COST_EN63A;
        running_cost_factor:            RUNNING_COST_EN63A;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1600 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        cargo_capacity:                 CARGO_CAPACITY_EN63A;
        weight:                         110 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.25*75/110;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   switch_en63a_purchase;
        default:                    switch_en63a_graphics;
        articulated_part:           switch_en63a_articulated;
        length:                     switch_en63a_length;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_en63a_capacity;
        visual_effect_and_powered:  switch_en63a_visual_effect;
        can_attach_wagon:           switch_en63a_can_attach;
        start_stop:                 switch_en63a_start_stop;
        ENGINE_RUNNING_COST_CALLBACKS(en63a)
        ENGINE_POWER_CALLBACKS(en63a)
    }
}
