// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* EU07 sprites */
spriteset(sset_eu07_purchase, "gfx/engines/eu07.png")           { tmpl_sprite_purchase(4, 60) }
spriteset(sset_eu07_purchase_yellow, "gfx/engines/eu07.png")    { tmpl_sprite_purchase(4, 120) }
spriteset(sset_eu07, "gfx/engines/eu07.png")                    { tmpl_sprite_train10(4, 20) }
spriteset(sset_eu07_yellow, "gfx/engines/eu07.png")             { tmpl_sprite_train10(4, 80) }
spriteset(sset_eu07_pkp_ic, "gfx/engines/eu07.png")             { tmpl_sprite_train10(4, 140) }
spriteset(sset_eu07_pkp_cargo, "gfx/engines/eu07.png")          { tmpl_sprite_train10(4, 180) }
spriteset(sset_eu07_pkp_pr1, "gfx/engines/eu07.png")            { tmpl_sprite_train10(4, 220) }
spriteset(sset_eu07_pkp_pr2, "gfx/engines/eu07.png")            { tmpl_sprite_train10(4, 260) }
spriteset(sset_eu07_pkp_polregio, "gfx/engines/eu07.png")       { tmpl_sprite_train10(4, 300) }

/* EU07 switches */
#define IS_EU07_YELLOW               build_year >= 1989
switch (FEAT_TRAINS, SELF, switch_eu07_subtype_text2, cargo_subtype) {
    0: return string(STR_SUBTYPE_LIVERY_CARGO_DEP);
    1: return string(STR_SUBTYPE_LIVERY_CLASSIC);
    2: return string(STR_SUBTYPE_LIVERY_PKP_CARGO);
    3: return string(STR_SUBTYPE_LIVERY_PKP_IC);
    4: return string(STR_SUBTYPE_LIVERY_PKP_PR);
    5: return string(STR_SUBTYPE_LIVERY_PKP_POLREGIO);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_eu07_subtype_text, PKP_LIVERIES_AVAILABLE) {
    1: return switch_eu07_subtype_text2;
    return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, switch_eu07_purchase, IS_EU07_YELLOW) {
    1: return sset_eu07_purchase_yellow;
    return sset_eu07_purchase;
}
switch(FEAT_TRAINS, SELF, switch_eu07_articulated, extra_callback_info1) {
    1: return item_eu07;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_eu07_length, position_in_vehid_chain % 2) {
    1: return 1;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_eu07_classic_graphics, IS_EU07_YELLOW) {
    1: return sset_eu07_yellow;
    return sset_eu07;
}
random_switch(FEAT_TRAINS, SELF, switch_eu07_pkp_pr3_graphics) {
    3: return sset_eu07_pkp_pr1;
    5: return sset_eu07_pkp_pr2;
}
switch(FEAT_TRAINS, SELF, switch_eu07_pkp_pr2_graphics, PKP_POLREGIO_LIVERY_AVAILABLE) {
    1: return sset_eu07_pkp_polregio;
    return switch_eu07_pkp_pr3_graphics;
}
switch(FEAT_TRAINS, SELF, switch_eu07_pkp_pr_graphics, PKP_PR_LIVERY_AVAILABLE) {
    1: return switch_eu07_pkp_pr2_graphics;
    return sset_eu07_pkp_pr1;
}
switch(FEAT_TRAINS, SELF, switch_eu07_auto2_graphics, bitmask_consist_info & VINFO_WAGON_TYPE) {
    bitmask(VINFO_CARGO_WAGON): return sset_eu07_pkp_cargo;
    bitmask(VINFO_PASSENGER_WAGON, VINFO_HIGH_SPEED_WAGON): return sset_eu07_pkp_ic;
    bitmask(VINFO_PASSENGER_WAGON): return switch_eu07_pkp_pr_graphics;
    return switch_eu07_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_eu07_auto_graphics, PKP_AUTO_LIVERY_AVAILABLE) {
    1: return switch_eu07_auto2_graphics;
    return switch_eu07_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_eu07_livery_graphics, cargo_subtype % 6) {
    0: return switch_eu07_auto_graphics;
//  1: return switch_eu07_classic_graphics;
    2: return sset_eu07_pkp_cargo;
    3: return sset_eu07_pkp_ic;
    4: return switch_eu07_pkp_pr_graphics;
    5: return sset_eu07_pkp_polregio;
    return switch_eu07_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_eu07_graphics, position_in_vehid_chain % 2) {
    0: return switch_eu07_livery_graphics;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_eu07_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_eu07_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_PASSENGER_FREIGHT));
    return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(eu07, EU07)
ENGINE_VISUAL_EFFECT_SWITCH(eu07)

switch (FEAT_TRAINS, SELF, switch_eu07_power2, position_in_consist > 2 ) { \
	1: return 1904; \
	return CB_FAILED; \
} \
switch (FEAT_TRAINS, PARENT, switch_eu07_power, PKPSET_ITEM_ID) { \
	item_asf: return 0; \
	item_ep01: return 1904; \
	item_ep02: return 1904; \
	item_ep05: return 1904; \
	item_et21: return 1904; \
	item_et22: return 1904; \
	item_et26: return 1904; \
	item_ep09: return 1904; \
	item_eu43: return 1904; \
	item_eu44: return 1904; \
	item_eu46: return 1904; \
	item_eu47: return 1904; \
	return switch_eu07_power2; \
}

/* Define the actual train */
item(FEAT_TRAINS, item_eu07) {
    property {
        /* common properties */
        name:                           string(STR_EU07_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1964, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_EU07;
        running_cost_factor:            RUNNING_COST_EU07;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          125 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          2000 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_REFIT_PROPERTY
        weight:                         81 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.284;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   switch_eu07_purchase;
        additional_text:            switch_eu07_add_text;
        default:                    switch_eu07_graphics;
        cargo_subtype_text:         switch_eu07_subtype_text;
        articulated_part:           switch_eu07_articulated;
        length:                     switch_eu07_length;
        visual_effect_and_powered:  switch_eu07_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_REFIT_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(eu07)
        ENGINE_POWER_CALLBACKS(eu07)
    }
}
