// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* SU160 (Gama) sprites */
spriteset(sset_su160_purchase, "gfx/su160-gama.png")    { tmpl_sprite_purchase(4, 60) }
spriteset(sset_su160, "gfx/su160-gama.png")             { tmpl_sprite_train10(4, 20) }

/* SU160 (Gama) switches */
switch(FEAT_TRAINS, SELF, switch_su160_articulated, extra_callback_info1) {
    1: return item_su160;       // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_su160_length, position_in_vehid_chain % 2) {
    1: return 2;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_su160_graphics, position_in_vehid_chain % 2) {
    0: return sset_su160;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_su160_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_su160_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_PASSENGER_FREIGHT));
	return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(su160, SU160)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(su160)
ENGINE_VISUAL_EFFECT_SWITCH(su160)

switch(FEAT_TRAINS, SELF, switch_su160_can_attach, vehicle_type_id) {
    item_twindexx:   return string(STR_ATTACH_DISALLOW, string(STR_SU160_NAME));
    item_twindexxcc: return string(STR_ATTACH_DISALLOW, string(STR_SU160_NAME));
    return switch_normal_can_attach;
}

/* Define the actual train */
item(FEAT_TRAINS, item_su160) {
    property {
		/* common properties */
        name:                           string(STR_SU160_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2015, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              30;     // 20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_SU160;
        running_cost_factor:            RUNNING_COST_SU160;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          2280 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         84 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.334;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_su160_purchase;
        additional_text:            switch_su160_add_text;
        default:                    switch_su160_graphics;
        articulated_part:           switch_su160_articulated;
        length:                     switch_su160_length;
        visual_effect_and_powered:  switch_su160_visual_effect;
        can_attach_wagon:           switch_su160_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(su160)
        ENGINE_POWER_CALLBACKS(su160)
    }
}
