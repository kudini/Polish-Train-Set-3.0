// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* OKl27 sprites */
spriteset(sset_okl27_purchase, "gfx/engines/okl27.png")     { tmpl_sprite_purchase(4, 60) }
spriteset(sset_okl27, "gfx/engines/okl27.png")              { tmpl_sprite_train(4, 20) }
spriteset(sset_okl27_rev, "gfx/engines/okl27.png")          { tmpl_sprite_train(4, 80) }

/* OKl27 switches */
switch(FEAT_TRAINS, PARENT, switch_okl27_graphics, vehicle_is_reversed) {
	1: return sset_okl27_rev;
    return sset_okl27;
}
switch(FEAT_TRAINS, PARENT, switch_okl27_visual_effect_local, vehicle_is_reversed) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_STEAM, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_okl27_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_MID), string(STR_ENGINE_PASSENGER));
	return string(STR_ENGINE_PASSENGER);
}
ENGINE_RUNNING_COST_SWITCH(okl27, OKL27)
STEAM_ENGINE_POWER_SWITCH(okl27, KW_TO_HP(684))
ENGINE_VISUAL_EFFECT_SWITCH(okl27)

/* Define the actual train */
item(FEAT_TRAINS, item_okl27) {
    property {
		/* common properties */
        name:                           string(STR_OKL27_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1927, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              18;     // 20
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_OKL27;
        running_cost_factor:            RUNNING_COST_OKL27;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          80 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          684 kW;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         81 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.160;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
	}
} else {
    property {
        model_life:                     58;
		retire_early:                   8;
	}
}

    graphics {
        purchase:                   sset_okl27_purchase;
        additional_text:            switch_okl27_add_text;
        default:                    switch_okl27_graphics;
        visual_effect_and_powered:  switch_okl27_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(okl27)
        ENGINE_POWER_CALLBACKS(okl27)
    }
}
