// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* ET26 (Dragon 2) sprites */
spriteset(sset_et26_purchase, "gfx/et26.png")  { tmpl_sprite_purchase(4, 60) }
spriteset(sset_et26, "gfx/et26.png")           { tmpl_sprite_train12(4, 20) }
spriteset(sset_et26_diesel, "gfx/et26.png")    { tmpl_sprite_train12(4, 80) }


/* ET26 (Dragon 2) switches */
switch(FEAT_TRAINS, SELF, switch_et26_articulated, extra_callback_info1) {
    1: return item_et26;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_et26_length, position_in_vehid_chain % 2) {
    1: return 3;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_et26_graphics_diesel, tile_has_catenary) {
    1: sset_et26;            // Electric mode
    return sset_et26_diesel;
}
switch(FEAT_TRAINS, SELF, switch_et26_graphics, position_in_vehid_chain % 2) {
    0: return switch_et26_graphics_diesel;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_et26_visual_effect_diesel, tile_has_catenary) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);    
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 3, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_et26_visual_effect_local, position_in_vehid_chain % 2) {
    0: return switch_et26_visual_effect_diesel;
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_et26_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_3L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_HEAVY_FREIGHT), string(STR_ET26_TEXT));
	return string(STR_PURCHASE_DESCR_2L, string(STR_ENGINE_HEAVY_FREIGHT), string(STR_ET26_TEXT));
}
ENGINE_RUNNING_COST_SWITCH(et26, ET26)
ENGINE_VISUAL_EFFECT_SWITCH(et26)

switch (FEAT_TRAINS, SELF, switch_et26_switch_power, tile_has_catenary) {
    1: return 6705; //hp
    return 724; //hp
}
/* switches for reducing power */
switch (FEAT_TRAINS, SELF, switch_et26_switch_power3, tile_has_catenary) {
    1: return 4694; //hp
    return 724; //hp
}
switch (FEAT_TRAINS, SELF, switch_et26_power2, position_in_consist > 0 ) {
	1: return switch_et26_switch_power3;
	return switch_et26_switch_power;
} 
switch (FEAT_TRAINS, PARENT, switch_et26_power, PKPSET_ITEM_ID) {
	item_asf: return 0;
	return switch_et26_power2;
}

/* Define the actual train */
item(FEAT_TRAINS, item_et26) {
    property {
		/* common properties */
        name:                           string(STR_ET26_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(2019, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_ET26;
        running_cost_factor:            RUNNING_COST_ET26;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          5000 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         120 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.3505;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_et26_purchase;
        additional_text:            switch_et26_add_text;
        power:                      switch_et26_power;
        purchase_power:             switch_et26_switch_power;
        default:                    switch_et26_graphics;
        articulated_part:           switch_et26_articulated;
        length:                     switch_et26_length;
        visual_effect_and_powered:  switch_et26_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(et26)
//        ENGINE_POWER_CALLBACKS(et26)
    }
}
