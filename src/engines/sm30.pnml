// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* SM30 sprites */
spriteset(sset_sm30_purchase, "gfx/sm30.png")           { tmpl_sprite_purchase(4, 60) }
spriteset(sset_sm30, "gfx/sm30.png")                    { tmpl_sprite_train(4, 20) }
spriteset(sset_sm30_rev, "gfx/sm30.png")                { tmpl_sprite_train(4, 80) }
spriteset(sset_sm30_pkp_cargo, "gfx/sm30.png")          { tmpl_sprite_train(4, 120) }
spriteset(sset_sm30_pkp_cargo_rev, "gfx/sm30.png")      { tmpl_sprite_train(4, 160) }

/* SM30 switches */
switch (FEAT_TRAINS, SELF, switch_sm30_subtype_text2, cargo_subtype) {
    0: return string(STR_SUBTYPE_LIVERY_CARGO_DEP);
    1: return string(STR_SUBTYPE_LIVERY_CLASSIC);
    2: return string(STR_SUBTYPE_LIVERY_PKP_CARGO);
}
switch (FEAT_TRAINS, SELF, switch_sm30_subtype_text, PKP_LIVERIES_AVAILABLE) {
    1: return switch_sm30_subtype_text2;
    return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, PARENT, switch_sm30_classic_graphics, vehicle_is_reversed) {
    1: return sset_sm30_rev;
    return sset_sm30;
}
switch(FEAT_TRAINS, PARENT, switch_sm30_pkp_cargo_graphics, vehicle_is_reversed) {
    1: return sset_sm30_pkp_cargo_rev;
    return sset_sm30_pkp_cargo;
}
switch(FEAT_TRAINS, SELF, switch_sm30_auto2_graphics, bitmask_consist_info & VINFO_WAGON_TYPE) {
    bitmask(VINFO_CARGO_WAGON): return switch_sm30_pkp_cargo_graphics;
    return switch_sm30_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_sm30_auto_graphics, PKP_AUTO_LIVERY_AVAILABLE) {
    1: return switch_sm30_auto2_graphics;
    return switch_sm30_classic_graphics;
}
switch(FEAT_TRAINS, PARENT, switch_sm30_graphics, cargo_subtype % 3) {
    0: return switch_sm30_auto_graphics;
//  1: return switch_sm30_classic_graphics;
    2: return switch_sm30_pkp_cargo_graphics;
    return switch_sm30_classic_graphics;
}
switch(FEAT_TRAINS, PARENT, switch_sm30_visual_effect_local, 0) {
    return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -1, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_sm30_add_text, IS_HEATING_CLIMATE) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_NONE_TEXT), string(STR_ENGINE_PASSENGER_FREIGHT));
    return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(sm30, SM30)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sm30)
ENGINE_VISUAL_EFFECT_SWITCH(sm30)

/* Dummy vehicle */
item(FEAT_TRAINS, item_sm30) {
    property {
        name:                           string(STR_SM30_NAME);
        climates_available:             PKP_CLIMATES;
        introduction_date:              date(1957, 1, 1);
        vehicle_life:                   35;
        reliability_decay:              10;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_SM30;
        running_cost_factor:            RUNNING_COST_SM30;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          60 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          257 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         36 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.208;
        air_drag_coefficient:           0.07;
        length:                         6;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     72;
        retire_early:                   16;
    }
}

    graphics {
        purchase:                   sset_sm30_purchase;
        additional_text:            switch_sm30_add_text;
        default:                    switch_sm30_graphics;
        visual_effect_and_powered:  switch_sm30_visual_effect;
        cargo_subtype_text:         switch_sm30_subtype_text;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(sm30)
        ENGINE_POWER_CALLBACKS(sm30)
    }
}
