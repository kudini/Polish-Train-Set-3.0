// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* EP09 sprites */
spriteset(sset_ep09_purchase, "gfx/ep09.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_ep09, "gfx/ep09.png")                { tmpl_sprite_train10(4, 20) }
spriteset(sset_ep09_pkp_ic, "gfx/ep09.png")         { tmpl_sprite_train10(4, 80) }
spriteset(sset_ep09_pkp_cargo, "gfx/ep09.png")      { tmpl_sprite_train10(4, 120) }

/* EP09 switches */
switch (FEAT_TRAINS, SELF, switch_ep09_subtype_text2, cargo_subtype) {
    0: return string(STR_SUBTYPE_LIVERY_CARGO_DEP);
    1: return string(STR_SUBTYPE_LIVERY_CLASSIC);
    2: return string(STR_SUBTYPE_LIVERY_PKP_CARGO);
    3: return string(STR_SUBTYPE_LIVERY_PKP_IC);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_ep09_subtype_text, PKP_LIVERIES_AVAILABLE) {
    1: return switch_ep09_subtype_text2;
    return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, switch_ep09_articulated, extra_callback_info1) {
    1: return item_ep09;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ep09_length, position_in_vehid_chain % 2) {
    1: return 1;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_ep09_auto2_graphics, bitmask_consist_info & VINFO_WAGON_TYPE) {
    bitmask(VINFO_CARGO_WAGON): return sset_ep09_pkp_cargo;
    bitmask(VINFO_PASSENGER_WAGON, VINFO_HIGH_SPEED_WAGON): return sset_ep09_pkp_ic;
    return sset_ep09;
}
switch(FEAT_TRAINS, SELF, switch_ep09_auto_graphics, PKP_AUTO_LIVERY_AVAILABLE) {
    1: return switch_ep09_auto2_graphics;
    return sset_ep09;
}
switch(FEAT_TRAINS, SELF, switch_ep09_livery_graphics, cargo_subtype % 4) {
    0: return switch_ep09_auto_graphics;
//  1: return sset_ep09;
    2: return sset_ep09_pkp_cargo;
    3: return sset_ep09_pkp_ic;
    return sset_ep09;
}
switch(FEAT_TRAINS, SELF, switch_ep09_graphics, position_in_vehid_chain % 2) {
    0: return switch_ep09_livery_graphics;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_ep09_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_ep09_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_EXPRESS));
    return string(STR_ENGINE_EXPRESS);
}
ENGINE_RUNNING_COST_SWITCH(ep09, EP09)
DIESEL_ELECTRIC_2_ENGINE_POWER_SWITCH(ep09, KW_TO_HP(2920))
ENGINE_VISUAL_EFFECT_SWITCH(ep09)

switch(FEAT_TRAINS, SELF, switch_ep09_can_attach, vehicle_type_id) {
    item_twindexx:   return string(STR_ATTACH_DISALLOW, string(STR_EP09_NAME));
    item_twindexxcc: return string(STR_ATTACH_DISALLOW, string(STR_EP09_NAME));
    return switch_normal_can_attach;
}

/* Define the actual train */
item(FEAT_TRAINS, item_ep09) {
    property {
        /* common properties */
        name:                           string(STR_EP09_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1986, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_EP09;
        running_cost_factor:            RUNNING_COST_EP09;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          2920 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_REFIT_PROPERTY
        weight:                         84 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.237;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_ep09_purchase;
        additional_text:            switch_ep09_add_text;
        default:                    switch_ep09_graphics;
        cargo_subtype_text:         switch_ep09_subtype_text;
        articulated_part:           switch_ep09_articulated;
        length:                     switch_ep09_length;
        visual_effect_and_powered:  switch_ep09_visual_effect;
        can_attach_wagon:           switch_ep09_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_REFIT_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(ep09)
        ENGINE_POWER_CALLBACKS(ep09)
    }
}
