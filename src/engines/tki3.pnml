// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* TKi3 sprites */
spriteset(sset_tki3_purchase, "gfx/tki3.png")       { tmpl_sprite_purchase(4, 60) }
spriteset(sset_tki3, "gfx/tki3.png")                { tmpl_sprite_train(4, 20) }
spriteset(sset_tki3_rev, "gfx/tki3.png")            { tmpl_sprite_train(4, 80) }

/* TKi3 switches */
switch(FEAT_TRAINS, PARENT, switch_tki3_graphics, vehicle_is_reversed) {
	1: return sset_tki3_rev;
    return sset_tki3;
}
switch(FEAT_TRAINS, PARENT, switch_tki3_visual_effect_local, vehicle_is_reversed) {
    1: return visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_tki3_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_LOW), string(STR_ENGINE_PASSENGER_FREIGHT));
	return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(tki3, TKI3)
STEAM_ENGINE_POWER_SWITCH(tki3, KW_TO_HP(345))
ENGINE_VISUAL_EFFECT_SWITCH(tki3)

/* Define the actual train */
item(FEAT_TRAINS, item_tki3) {
    property {
		/* common properties */
        name:                           string(STR_TKI3_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              PKP_INTRODUCTION_DATE;
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   36;     // 20;
        reliability_decay:              16;     // 20
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_TKI3;
        running_cost_factor:            RUNNING_COST_TKI3;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          65 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          345 kW;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         59 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.151;
        air_drag_coefficient:           0.07;
        length:                         7;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_orig_intro_dates) {
    property {
        introduction_date:              date(1901, 1, 1);
    }
}

if(!param_keep_old_vehs) {
    if(param_orig_intro_dates) {
        property {
            model_life:                 57+17;
            retire_early:               12;
        }
    } else {
        property {
            model_life:                 57;
            retire_early:               12;
        }
    }
}

    graphics {
        purchase:                   sset_tki3_purchase;
        additional_text:            switch_tki3_add_text;
        default:                    switch_tki3_graphics;
        visual_effect_and_powered:  switch_tki3_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(tki3)
        ENGINE_POWER_CALLBACKS(tki3)
    }
}
