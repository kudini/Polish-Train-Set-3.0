// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* ET41 sprites */
spriteset(sset_et41_purchase, "gfx/et41.png")           { tmpl_sprite_purchase(4, 100) }
spriteset(sset_et41_purchase_yellow, "gfx/et41.png")    { tmpl_sprite_purchase(4, 200) }
spriteset(sset_et41_head, "gfx/et41.png")               { tmpl_sprite_train10(4, 20) }
spriteset(sset_et41_tail, "gfx/et41.png")               { tmpl_sprite_train10_offset(4, 60, 1) }
spriteset(sset_et41_head_yellow, "gfx/et41.png")        { tmpl_sprite_train10(4, 120) }
spriteset(sset_et41_tail_yellow, "gfx/et41.png")        { tmpl_sprite_train10_offset(4, 160, 1) }
spriteset(sset_et41_head_pkp_cargo, "gfx/et41.png")     { tmpl_sprite_train10(4, 220) }
spriteset(sset_et41_tail_pkp_cargo, "gfx/et41.png")     { tmpl_sprite_train10_offset(4, 260, 1) }

/* ET41 switches */
#define IS_ET41_YELLOW  build_year >= 1989
switch (FEAT_TRAINS, SELF, switch_et41_subtype_text2, cargo_subtype) {
    0: return string(STR_SUBTYPE_LIVERY_CARGO_DEP);
    1: return string(STR_SUBTYPE_LIVERY_CLASSIC);
    2: return string(STR_SUBTYPE_LIVERY_PKP_CARGO);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_et41_subtype_text, PKP_LIVERIES_AVAILABLE) {
    1: return switch_et41_subtype_text2;
    return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, switch_et41_purchase, IS_ET41_YELLOW) {
    1: return sset_et41_purchase_yellow;
    return sset_et41_purchase;
}
switch(FEAT_TRAINS, SELF, switch_et41_articulated, extra_callback_info1) {
    1..2: return item_et41;     // add second part
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_et41_length, position_in_vehid_chain % 3) {
    1: return 2;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_et41_head_classic_graphics, IS_ET41_YELLOW) {
    1: return sset_et41_head_yellow;
    return sset_et41_head;
}
switch(FEAT_TRAINS, SELF, switch_et41_head_auto2_graphics, bitmask_consist_info & VINFO_WAGON_TYPE) {
    bitmask(VINFO_CARGO_WAGON): return sset_et41_head_pkp_cargo;
    return switch_et41_head_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_et41_head_auto_graphics, PKP_AUTO_LIVERY_AVAILABLE) {
    1: return switch_et41_head_auto2_graphics;
    return switch_et41_head_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_et41_head_livery_graphics, cargo_subtype % 3) {
    0: return switch_et41_head_auto_graphics;
//  1: return switch_et41_head_classic_graphics;
    2: return sset_et41_head_pkp_cargo;
    return switch_et41_head_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_et41_tail_classic_graphics, IS_ET41_YELLOW) {
    1: return sset_et41_tail_yellow;
    return sset_et41_tail;
}
switch(FEAT_TRAINS, SELF, switch_et41_tail_auto2_graphics, bitmask_consist_info & VINFO_WAGON_TYPE) {
    bitmask(VINFO_CARGO_WAGON): return sset_et41_tail_pkp_cargo;
    return switch_et41_tail_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_et41_tail_auto_graphics, PKP_AUTO_LIVERY_AVAILABLE) {
    1: return switch_et41_tail_auto2_graphics;
    return switch_et41_tail_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_et41_tail_livery_graphics, cargo_subtype % 3) {
    0: return switch_et41_tail_auto_graphics;
//  1: return switch_et41_tail_classic_graphics;
    2: return sset_et41_tail_pkp_cargo;
    return switch_et41_tail_classic_graphics;
}
switch(FEAT_TRAINS, SELF, switch_et41_graphics, position_in_vehid_chain % 3) {
    0: return switch_et41_head_livery_graphics;
    2: return switch_et41_tail_livery_graphics;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_et41_visual_effect_local, position_in_vehid_chain % 3) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    2: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_et41_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_HI), string(STR_ENGINE_HEAVY_FREIGHT));
	return string(STR_ENGINE_HEAVY_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(et41, ET41)
DIESEL_ELECTRIC_2_ENGINE_POWER_SWITCH(et41, KW_TO_HP(4000))
ENGINE_VISUAL_EFFECT_SWITCH(et41)

/* Define the actual train */
item(FEAT_TRAINS, item_et41) {
    property {
		/* common properties */
        name:                           string(STR_ET41_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1977, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_ET41;
        running_cost_factor:            RUNNING_COST_ET41;
		/* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          125 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          4000 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_REFIT_PROPERTY
        weight:                         167 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.284;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   switch_et41_purchase;
        additional_text:            switch_et41_add_text;
        default:                    switch_et41_graphics;
        cargo_subtype_text:         switch_et41_subtype_text;
        articulated_part:           switch_et41_articulated;
        length:                     switch_et41_length;
        visual_effect_and_powered:  switch_et41_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_REFIT_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(et41)
        ENGINE_POWER_CALLBACKS(et41)
    }
}
