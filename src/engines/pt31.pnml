// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, GPL 2.0

/* Pt31 sprites */
spriteset(sset_pt31_purchase, "gfx/pt31.png")   { tmpl_sprite_purchase(4, 100) }
spriteset(sset_pt31, "gfx/pt31.png")            { tmpl_sprite_train10(4, 20) }
spriteset(sset_pt31_tender, "gfx/pt31.png")     { tmpl_sprite_train(4, 60) }

/* Pt31 switches */
switch(FEAT_TRAINS, SELF, switch_pt31_articulated, extra_callback_info1) {
    1: return item_pt31;    // add tender
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_pt31_graphics, position_in_vehid_chain % 2) {
    0: return sset_pt31;
    return sset_pt31_tender;
}
switch(FEAT_TRAINS, SELF, switch_pt31_length, position_in_vehid_chain % 2) {
    1: return 7;
    return 8;
}
switch (FEAT_TRAINS, SELF, switch_pt31_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_HI), string(STR_ENGINE_EXPRESS));
	return string(STR_ENGINE_EXPRESS);
}
ENGINE_RUNNING_COST_SWITCH(pt31, PT31)
STEAM_ENGINE_POWER_SWITCH(pt31, KW_TO_HP(1421))

/* Define the actual train */
item(FEAT_TRAINS, item_pt31) {
    property {
        /* common properties */
        name:                           string(STR_PT31_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1931, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION 
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_PT31;
        running_cost_factor:            RUNNING_COST_PT31;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          110 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1421 kW;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         173 ton;    // 106+67 ton
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.080;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
	}
} else {
    property {
        model_life:                     57;
		retire_early:                   8;
	}
}

    graphics {
        purchase:                   sset_pt31_purchase;
        additional_text:            switch_pt31_add_text;
        default:                    switch_pt31_graphics;
        articulated_part:           switch_pt31_articulated;
        length:                     switch_pt31_length;
        visual_effect_and_powered:  switch_normal_steam_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(pt31)
        ENGINE_POWER_CALLBACKS(pt31)
    }
}
