// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* ED74 sprites */
spriteset(sset_ed74_purchase, "gfx/engines/ed74.png")   { tmpl_sprite_purchase(4, 180) }
spriteset(sset_ed74_advert, "gfx/engines/ed74.png")     { tmpl_sprite_advertisement200(4, 360) }

spriteset(sset_ed74_head, "gfx/engines/ed74.png")       { tmpl_sprite_train12(4, 20) }
spriteset(sset_ed74_mid1, "gfx/engines/ed74.png")       { tmpl_sprite_train10_offset(4, 60, 1) }
spriteset(sset_ed74_mid2, "gfx/engines/ed74.png")       { tmpl_sprite_train10_offset(4, 100, 1) }
spriteset(sset_ed74_tail, "gfx/engines/ed74.png")       { tmpl_sprite_train12_offset(4, 140, 1) }
spriteset(sset_ed74_head_open, "gfx/engines/ed74.png")  { tmpl_sprite_train12(4, 200) }
spriteset(sset_ed74_mid1_open, "gfx/engines/ed74.png")  { tmpl_sprite_train10_offset(4, 240, 1) }
spriteset(sset_ed74_mid2_open, "gfx/engines/ed74.png")  { tmpl_sprite_train10_offset(4, 280, 1) }
spriteset(sset_ed74_tail_open, "gfx/engines/ed74.png")  { tmpl_sprite_train12_offset(4, 320, 1) }
spritegroup sgroup_ed74_head {
    loaded:     [sset_ed74_head];
    loading:    [sset_ed74_head_open];
}
spritegroup sgroup_ed74_mid1 {
    loaded:     [sset_ed74_mid1];
    loading:    [sset_ed74_mid1_open];
}
spritegroup sgroup_ed74_mid2 {
    loaded:     [sset_ed74_mid2];
    loading:    [sset_ed74_mid2_open];
}
spritegroup sgroup_ed74_tail {
    loaded:     [sset_ed74_tail];
    loading:    [sset_ed74_tail_open];
}

/* ED74 switches */
switch(FEAT_TRAINS, SELF, switch_ed74_articulated, extra_callback_info1) {
    1..6: return item_ed74;     // including dummy parts
    7: return item_dummy_2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ed74_length, position_in_vehid_chain % 7) {
    1: return 4;    // dummy part
    3: return 2;    // dummy part
    5: return 2;    // dummy part
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_ed74_purchase, extra_callback_info1 & 0xFF) {
    0x21: return sset_ed74_advert;
    return sset_ed74_purchase;
}
switch(FEAT_TRAINS, SELF, switch_ed74_graphics, position_in_vehid_chain % 7) {
    0: return sgroup_ed74_head;
    2: return sgroup_ed74_mid1;
    4: return sgroup_ed74_mid2;
    6: return sgroup_ed74_tail;
    return sset_dummy;      // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_ed74_capacity, position_in_vehid_chain % 7) {
    0: return 47-1;
    2: return 54-1;
    4: return 54-1;
    6: return 47;
    return 1;   // dummy parts
}
switch(FEAT_TRAINS, SELF, switch_ed74_visual_effect_local, position_in_vehid_chain % 7) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 3, DISABLE_WAGON_POWER);
    5: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, -3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_ed74_can_attach, vehicle_type_id) {
    item_en57:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_en71:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed72:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed74:  return CB_RESULT_ATTACH_ALLOW;
    item_en76:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed250:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_en63a:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed161:  return string(STR_ATTACH_DISALLOW_EZT2);
    return string(STR_ATTACH_DISALLOW_EZT1);
}
switch (FEAT_TRAINS, SELF, switch_ed74_start_stop, num_vehs_in_consist / 7) {
    1..2: return CB_RESULT_NO_TEXT;
    return string(STR_TRAIN_TOO_LONG, string(STR_ED74_NAME));
}
switch(FEAT_TRAINS, SELF, switch_ed74_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_3L, string(STR_HEATING_ELECTRIC_TEXT), string(STR_ED74_TEXT), string(STR_INCOME_1_CLASS));
    return string(STR_PURCHASE_DESCR_2L, string(STR_ED74_TEXT), string(STR_INCOME_1_CLASS));
}
ENGINE_RUNNING_COST_SWITCH(ed74, ED74)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(ed74)
ENGINE_VISUAL_EFFECT_SWITCH(ed74)

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#define SITTING_PLACES 47+54+54+47
#define CARGO_CAPACITY_ED74    int((SITTING_PLACES+1)/7)

#define AGE_ED74_2_CLASS   185
#define AGE_ED74_1_CLASS   185*2.82

/* Define the actual train */
item(FEAT_TRAINS, item_ed74) {
    property {
	name:                           string(STR_ED74_NAME);
        climates_available:             PKP_CLIMATES;
        introduction_date:              date(2007, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  18;     // per part
	cost_factor:                    PURCHASE_COST_ED74;
        running_cost_factor:            RUNNING_COST_ED74;
        cargo_age_period:               int((33*AGE_ED74_1_CLASS + (202-33)*AGE_ED74_2_CLASS)/202);
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          160 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          2000 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        cargo_capacity:                 CARGO_CAPACITY_ED74;
        weight:                         159 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.25*90/159;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }

    graphics {
        purchase:                   switch_ed74_purchase;
        additional_text:            switch_ed74_add_text;
        default:                    switch_ed74_graphics;
        articulated_part:           switch_ed74_articulated;
        length:                     switch_ed74_length;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_ed74_capacity;
        visual_effect_and_powered:  switch_ed74_visual_effect;
        can_attach_wagon:           switch_ed74_can_attach;
        start_stop:                 switch_ed74_start_stop;
        ENGINE_RUNNING_COST_CALLBACKS(ed74)
        ENGINE_POWER_CALLBACKS(ed74)
    }
}
