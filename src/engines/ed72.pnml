// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* ED72 sprites */
spriteset(sset_ed72_purchase, "gfx/ed72.png")       { tmpl_sprite_purchase(4, 180) }
spriteset(sset_ed72_advert, "gfx/ed72.png")         { tmpl_sprite_advertisement200(4, 360) }

spriteset(sset_ed72_head, "gfx/ed72.png")           { tmpl_sprite_train12(4, 20) }
spriteset(sset_ed72_mid1, "gfx/ed72.png")           { tmpl_sprite_train10_offset(4, 60, 1) }
spriteset(sset_ed72_mid2, "gfx/ed72.png")           { tmpl_sprite_train10_offset(4, 100, 1) }
spriteset(sset_ed72_tail, "gfx/ed72.png")           { tmpl_sprite_train12_offset(4, 140, 1) }
spriteset(sset_ed72_head_open, "gfx/ed72.png")      { tmpl_sprite_train12(4, 200) }
spriteset(sset_ed72_mid1_open, "gfx/ed72.png")      { tmpl_sprite_train10_offset(4, 240, 1) }
spriteset(sset_ed72_mid2_open, "gfx/ed72.png")      { tmpl_sprite_train10_offset(4, 280, 1) }
spriteset(sset_ed72_tail_open, "gfx/ed72.png")      { tmpl_sprite_train12_offset(4, 320, 1) }
spritegroup sgroup_ed72_head {
    loaded:     [sset_ed72_head];
    loading:    [sset_ed72_head_open];
}
spritegroup sgroup_ed72_mid1 {
    loaded:     [sset_ed72_mid1];
    loading:    [sset_ed72_mid1_open];
}
spritegroup sgroup_ed72_mid2 {
    loaded:     [sset_ed72_mid2];
    loading:    [sset_ed72_mid2_open];
}
spritegroup sgroup_ed72_tail {
    loaded:     [sset_ed72_tail];
    loading:    [sset_ed72_tail_open];
}

/* ED72 switches */
switch(FEAT_TRAINS, SELF, switch_ed72_purchase, extra_callback_info1 & 0xFF) {
    0x21: return sset_ed72_advert;
    return sset_ed72_purchase;
}
switch(FEAT_TRAINS, SELF, switch_ed72_articulated, extra_callback_info1) {
    1..6: return item_ed72;
    7: return item_dummy_2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ed72_length, position_in_vehid_chain % 7) {
    1: return 4;    // dummy part
    3: return 2;    // dummy part
    5: return 2;    // dummy part
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_ed72_tour_capacity, position_in_vehid_chain % 7) {
    0: return 50;
    2: return 70-2;
    4: return 70-1;
    6: return 50;
    return 1;
}
switch(FEAT_TRAINS, SELF, switch_ed72_pass_capacity, position_in_vehid_chain % 7) {
    0: return 50+25;   //50;
    2: return 70+24-2; //70;
    4: return 70+24-1; //70;
    6: return 50+25;   //50;
    return 1;
}
switch(FEAT_TRAINS, SELF, switch_ed72_capacity, cargo_type_in_veh) {
    TOUR: return switch_ed72_tour_capacity;
    return switch_ed72_pass_capacity;
}
switch(FEAT_TRAINS, SELF, switch_ed72_graphics, STORE_TEMP(position_in_vehid_chain % 7, 0x7F)) {
    0: return sgroup_ed72_head;
    2: return sgroup_ed72_mid1;
    4: return sgroup_ed72_mid2;
    6: return sgroup_ed72_tail;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_ed72_visual_effect_local, position_in_vehid_chain % 7) {
    2: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    4: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_ed72_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_3L, string(STR_HEATING_ELECTRIC_TEXT), string(STR_ED72_TEXT), string(STR_INCOME_1_CLASS));
    return string(STR_PURCHASE_DESCR_2L, string(STR_ED72_TEXT), string(STR_INCOME_1_CLASS));
}
switch(FEAT_TRAINS, SELF, switch_ed72_can_attach, vehicle_type_id) {
    item_en57:  return CB_RESULT_ATTACH_ALLOW;
    item_en71:  return CB_RESULT_ATTACH_ALLOW;
    item_ed72:  return CB_RESULT_ATTACH_ALLOW;
    item_ed74:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_en76:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed250:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_en63a:  return string(STR_ATTACH_DISALLOW_EZT2);
    item_ed161:  return string(STR_ATTACH_DISALLOW_EZT2);
    return string(STR_ATTACH_DISALLOW_EZT1);
}
switch (FEAT_TRAINS, SELF, switch_ed72_start_stop, COUNT_EZT > 3) {
    1: return string(STR_TRAIN_TOO_LONG, string(STR_ED72_NAME));
    return CB_RESULT_NO_TEXT;
}
ENGINE_RUNNING_COST_SWITCH(ed72, ED72)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(ed72)
ENGINE_VISUAL_EFFECT_SWITCH(ed72)

#ifdef SITTING_PLACES
    #undef SITTING_PLACES
#endif
#ifdef STANDING_PLACES
    #undef STANDING_PLACES
#endif
#define SITTING_PLACES 50*2+70*2
#define STANDING_PLACES 25*2+24*2
#define CARGO_CAPACITY_ED72     int((SITTING_PLACES+STANDING_PLACES+3)/7)

#define AGE_ED72_2_CLASS    185
#define AGE_ED72_1_CLASS    185*2.82

/* Define the actual train */
item(FEAT_TRAINS, item_ed72) {
    property {
        /* common properties */
        name:                           string(STR_ED72_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1993, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_PASSENGERS_WAGON_DEFINITION
        loading_speed:                  21;     // per part
        cost_factor:                    PURCHASE_COST_ED72;
        running_cost_factor:            RUNNING_COST_ED72;
        cargo_age_period:               int((50*AGE_ED72_1_CLASS + (338-50)*AGE_ED72_2_CLASS)/338);
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          130 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1400 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        cargo_capacity:                 CARGO_CAPACITY_ED72;
        weight:                         182 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.110;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           bitmask(VINFO_PASSENGER_WAGON);
    }
    graphics {
        purchase:                   switch_ed72_purchase;
        additional_text:            switch_ed72_add_text;
        default:                    switch_ed72_graphics;
//        cargo_subtype_text:         switch_enxx_subtype_text;
        articulated_part:           switch_ed72_articulated;
        length:                     switch_ed72_length;
        visual_effect_and_powered:  switch_ed72_visual_effect;
        purchase_cargo_capacity:    return CB_FAILED;
        cargo_capacity:             switch_ed72_capacity;
        can_attach_wagon:           switch_ed72_can_attach;
        start_stop:                 switch_ed72_start_stop;
        ENGINE_RUNNING_COST_CALLBACKS(ed72)
        ENGINE_POWER_CALLBACKS(ed72)
    }
}
