// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Pt47 sprites */
spriteset(sset_pt47_purchase, "gfx/engines/pt47.png")       { tmpl_sprite_purchase(4, 100) }
spriteset(sset_pt47_f1, "gfx/engines/pt47.png")             { tmpl_sprite_train10(4, 20) }
spriteset(sset_pt47_f2, "gfx/engines/pt47.png")             { tmpl_sprite_train10(4, 20) }
spriteset(sset_pt47_tender, "gfx/engines/pt47.png")         { tmpl_sprite_train(4, 60) }
#ifdef ZOOM_SPRITES_ENABLED
alternative_sprites(sset_pt47_f1,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/engines/pt47-x2.png")    { tmpl_sprite_train10_x2(8, 40) }
alternative_sprites(sset_pt47_f2,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/engines/pt47-x2.png")    { tmpl_sprite_train10_x2(8, 120) }
alternative_sprites(sset_pt47_tender,
    ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/engines/pt47-x2.png")    { tmpl_sprite_train_x2(8, 200) }
#endif

/* Pt47 switches */
switch(FEAT_TRAINS, SELF, switch_pt47_articulated, extra_callback_info1) {
    1: return item_pt47;    // add tender
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_pt47_graphics_frames, motion_counter % 4) {
    0..1: return sset_pt47_f1;
    return sset_pt47_f2;
}
switch(FEAT_TRAINS, SELF, switch_pt47_graphics, position_in_vehid_chain % 2) {
    0: return switch_pt47_graphics_frames;
    return sset_pt47_tender;
}
switch(FEAT_TRAINS, SELF, switch_pt47_length, position_in_vehid_chain % 2) {
    1: return 7;
    return 8;
}
switch (FEAT_TRAINS, SELF, switch_pt47_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_HI), string(STR_ENGINE_EXPRESS));
    return string(STR_ENGINE_EXPRESS);
}
ENGINE_RUNNING_COST_SWITCH(pt47, PT47)
STEAM_ENGINE_POWER_SWITCH(pt47, KW_TO_HP(1471))

/* Define the actual train */
item(FEAT_TRAINS, item_pt47) {
    property {
        /* common properties */
        name:                           string(STR_PT47_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1947, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_PT47;
        running_cost_factor:            RUNNING_COST_PT47;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1471 kW;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         175 ton;    // 103+72 t
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.079;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     64;
        retire_early:                   16;
    }
}

    graphics {
        purchase:                   sset_pt47_purchase;
        additional_text:            switch_pt47_add_text;
        default:                    switch_pt47_graphics;
        articulated_part:           switch_pt47_articulated;
        length:                     switch_pt47_length;
        visual_effect_and_powered:  switch_normal_steam_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(pt47)
        ENGINE_POWER_CALLBACKS(pt47)
    }
}
