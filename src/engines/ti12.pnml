// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Ti12 sprites */
spriteset(sset_ti12_purchase, "gfx/ti12.png")   { tmpl_sprite_purchase(4, 100) }
spriteset(sset_ti12, "gfx/ti12.png")            { tmpl_sprite_train(4, 20) }
spriteset(sset_ti12_tender, "gfx/ti12.png")     { tmpl_sprite_train(4, 60) }

/* Ti12 switches */
switch(FEAT_TRAINS, SELF, switch_ti12_articulated, extra_callback_info1) {
    1: return item_ti12;    // add tender
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_ti12_graphics, position_in_vehid_chain % 2) {
    0: return sset_ti12;
    return sset_ti12_tender;
}
switch(FEAT_TRAINS, SELF, switch_ti12_length, position_in_vehid_chain % 2) {
    1: return 4;
    return 6;
}
switch (FEAT_TRAINS, SELF, switch_ti12_add_text, IS_HEATING_CLIMATE ) {
	1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_LOW), string(STR_ENGINE_FREIGHT));
	return string(STR_ENGINE_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(ti12, TI12)
STEAM_ENGINE_POWER_SWITCH(ti12, KW_TO_HP(482))

/* Define the actual train */
item(FEAT_TRAINS, item_ti12) {
    property {
        /* common properties */
        name:                           string(STR_TI12_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              PKP_INTRODUCTION_DATE;
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   33;     // 20;
        reliability_decay:              16;     // 20
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_TI12;
        running_cost_factor:            RUNNING_COST_TI12;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          60 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          482 kW;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         87 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.099;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_orig_intro_dates) {
    property {
        introduction_date:              date(1897, 1, 1);
    }
}

if(!param_keep_old_vehs) {
    if(param_orig_intro_dates) {
        property {
            model_life:                 48+21;
            retire_early:               12;
        }
    } else {
        property {
            model_life:                 48;
            retire_early:               12;
        }
    }
}

    graphics {
        purchase:                   sset_ti12_purchase;
        additional_text:            switch_ti12_add_text;
        default:                    switch_ti12_graphics;
        articulated_part:           switch_ti12_articulated;
        length:                     switch_ti12_length;
        visual_effect_and_powered:  switch_normal_steam_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(ti12)
        ENGINE_POWER_CALLBACKS(ti12)
    }
}
