// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* SP45 sprites */
spriteset(sset_sp45_purchase, "gfx/sp45.png")           { tmpl_sprite_purchase(4, 60) }
spriteset(sset_sp45, "gfx/sp45.png")                    { tmpl_sprite_train10(4, 20) }

/* SP45 switches */
switch(FEAT_TRAINS, SELF, switch_sp45_articulated, extra_callback_info1) {
    1: return item_sp45;        // dummy parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_sp45_length, position_in_vehid_chain % 2) {
    1: return 2;                // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_sp45_graphics, position_in_vehid_chain % 2) {
    0: return sset_sp45;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_sp45_visual_effect_local, position_in_vehid_chain % 2) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 1, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, SELF, switch_sp45_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_HI), string(STR_ENGINE_PASSENGER_FREIGHT));
    return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(sp45, SP45)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sp45)
ENGINE_VISUAL_EFFECT_SWITCH(sp45)

/* Define the actual train */
item(FEAT_TRAINS, item_sp45) {
    property {
        /* common properties */
        name:                           string(STR_SP45_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1970, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5; 
        cost_factor:                    PURCHASE_COST_SU45;
        running_cost_factor:            RUNNING_COST_SU45;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          120 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          1250 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         102 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.302;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     49;
        retire_early:                   16;
    }
}

    graphics {
        purchase:                   sset_sp45_purchase;
        additional_text:            switch_sp45_add_text;
        default:                    switch_sp45_graphics;
        articulated_part:           switch_sp45_articulated;
        length:                     switch_sp45_length;
        visual_effect_and_powered:  switch_sp45_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(sp45)
        ENGINE_POWER_CALLBACKS(sp45)
    }
}