// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* SM42/SP42/SU42 sprites */
spriteset(sset_sm42_purchase, "gfx/engines/sm42.png")   { tmpl_sprite_purchase(4, 60) }
spriteset(sset_sm42, "gfx/engines/sm42.png")            { tmpl_sprite_train(4, 20) }
spriteset(sset_sp42_purchase, "gfx/engines/sm42.png")   { tmpl_sprite_purchase(4, 120) }
spriteset(sset_sp42, "gfx/engines/sm42.png")            { tmpl_sprite_train(4, 80) }
spriteset(sset_su42_purchase, "gfx/engines/sm42.png")   { tmpl_sprite_purchase(4, 180) }
spriteset(sset_su42, "gfx/engines/sm42.png")            { tmpl_sprite_train(4, 140) }
spriteset(sset_sm42_pkp_cargo, "gfx/engines/sm42.png")  { tmpl_sprite_train(4, 200) }
spriteset(sset_sm42_pkp_ic, "gfx/engines/sm42ic.png")   { tmpl_sprite_train(4, 20) }

/* SM42/SP42/SU42 switches */
#define IS_SM42_NEW_LIVERY(year)    (build_year >= year) || (date_of_last_service >= date(year, 1, 1))
switch (FEAT_TRAINS, SELF, switch_sm42_subtype_text2, cargo_subtype) {
    0: return string(STR_SUBTYPE_LIVERY_CARGO_DEP);
    1: return string(STR_SUBTYPE_LIVERY_CLASSIC);
    2: return string(STR_SUBTYPE_LIVERY_PKP_CARGO);
    3: return string(STR_SUBTYPE_LIVERY_PKP_IC);
    return CB_RESULT_NO_TEXT;
}
switch (FEAT_TRAINS, SELF, switch_sm42_subtype_text, PKP_LIVERIES_AVAILABLE) {
    1: return switch_sm42_subtype_text2;
    return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, switch_sm42_auto2_graphics, bitmask_consist_info & VINFO_WAGON_TYPE) {
    bitmask(VINFO_CARGO_WAGON): return sset_sm42_pkp_cargo;
    bitmask(VINFO_PASSENGER_WAGON, VINFO_HIGH_SPEED_WAGON): return sset_sm42_pkp_ic;
    return sset_sm42;
}
switch(FEAT_TRAINS, SELF, switch_sm42_auto_graphics, IS_SM42_NEW_LIVERY(2001)) {
    1: return switch_sm42_auto2_graphics;
    return sset_sm42;
}
switch(FEAT_TRAINS, SELF, switch_sm42_graphics, cargo_subtype % 4) {
    0: return switch_sm42_auto_graphics;
//  1: return sset_sm42;
    2: return sset_sm42_pkp_cargo;
    3: return sset_sm42_pkp_ic;
    return sset_sm42;
}
switch(FEAT_TRAINS, PARENT, switch_sm42_visual_effect_local, 0) {
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, PARENT, switch_sp42_visual_effect_local, 0) {
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, DISABLE_WAGON_POWER);
}
switch(FEAT_TRAINS, PARENT, switch_su42_visual_effect_local, 0) {
    return visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, DISABLE_WAGON_POWER);
}

switch (FEAT_TRAINS, SELF, switch_sm42_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_NONE_TEXT), string(STR_ENGINE_PASSENGER_FREIGHT));
    return string(STR_ENGINE_PASSENGER_FREIGHT);
}
switch (FEAT_TRAINS, SELF, switch_sp42_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_STEAM_POWER_TEXT, HEATING_POWER_STEAM_MID), string(STR_ENGINE_PASSENGER_FREIGHT));
    return string(STR_ENGINE_PASSENGER_FREIGHT);
}
switch (FEAT_TRAINS, SELF, switch_su42_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_LOW), string(STR_ENGINE_PASSENGER_FREIGHT));
    return string(STR_ENGINE_PASSENGER_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(sm42, SM42)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sm42)
ENGINE_VISUAL_EFFECT_SWITCH(sm42)
ENGINE_RUNNING_COST_SWITCH(sp42, SP42)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(sp42)
ENGINE_VISUAL_EFFECT_SWITCH(sp42)
ENGINE_RUNNING_COST_SWITCH(su42, SU42)
DIESEL_ELECTRIC_ENGINE_POWER_SWITCH(su42)
ENGINE_VISUAL_EFFECT_SWITCH(su42)

/* Define the actual trains */
item(FEAT_TRAINS, item_sm42) {
    property {
        /* common properties */
        name:                           string(STR_SM42_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1965, 1, 1);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_SM42;
        running_cost_factor:            RUNNING_COST_SM42;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          90 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          588 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_REFIT_PROPERTY
        weight:                         70 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.300;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

    graphics {
        purchase:                   sset_sm42_purchase;
        additional_text:            switch_sm42_add_text;
        default:                    switch_sm42_graphics;
        cargo_subtype_text:         switch_sm42_subtype_text;
        visual_effect_and_powered:  switch_sm42_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_REFIT_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(sm42)
        ENGINE_POWER_CALLBACKS(sm42)
    }
}

item(FEAT_TRAINS, item_sp42) {
    property {
        /* common properties */
        name:                           string(STR_SP42_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1967, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_SP42;
        running_cost_factor:            RUNNING_COST_SP42;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          90 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          582 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         70 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.300;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     55;
        retire_early:                   16;
    }
}

    graphics {
        purchase:                   sset_sp42_purchase;
        additional_text:            switch_sp42_add_text;
        default:                    sset_sp42;
        visual_effect_and_powered:  switch_sp42_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(sp42)
        ENGINE_POWER_CALLBACKS(sp42)
    }
}

item(FEAT_TRAINS, item_su42) {
    property {
        /* common properties */
        name:                           string(STR_SU42_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1999, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_SU42;
        running_cost_factor:            RUNNING_COST_SU42;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          90 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          590 kW;
        running_cost_base:              RUNNING_COST_DIESEL;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         70 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.300;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }
    
if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     51;
        retire_early:                   16;
    }
}

    graphics {
        purchase:                   sset_su42_purchase;
        additional_text:            switch_su42_add_text;
        default:                    sset_su42;
        visual_effect_and_powered:  switch_su42_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(su42)
        ENGINE_POWER_CALLBACKS(su42)
    }
}
