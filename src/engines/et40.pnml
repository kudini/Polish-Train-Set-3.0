// This is part of Polish PKP Set 2.0 NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* ET40 sprites */
spriteset(sset_et40_purchase, "gfx/engines/et40.png")           { tmpl_sprite_purchase(4, 100) }
spriteset(sset_et40_head, "gfx/engines/et40.png")               { tmpl_sprite_train10(4, 20) }
spriteset(sset_et40_tail, "gfx/engines/et40.png")               { tmpl_sprite_train10_offset(4, 60, 1) }

/* ET40 switches */
switch(FEAT_TRAINS, SELF, switch_et40_articulated, extra_callback_info1) {
    1..2: return item_et40;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, switch_et40_length, position_in_vehid_chain % 3) {
    1: return 2;    // dummy parts
    return 8;
}
switch(FEAT_TRAINS, SELF, switch_et40_graphics, position_in_vehid_chain % 3) {
    0: return sset_et40_head;
    2: return sset_et40_tail;
    return sset_dummy;
}
switch(FEAT_TRAINS, SELF, switch_et40_visual_effect_local, position_in_vehid_chain % 3) {
    2: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
switch (FEAT_TRAINS, SELF, switch_et40_add_text, IS_HEATING_CLIMATE ) {
    1: return string(STR_PURCHASE_DESCR_2L, string(STR_HEATING_ELECTRIC_POWER_TEXT, HEATING_POWER_ELECTRIC_MID), string(STR_ENGINE_HEAVY_FREIGHT));
    return string(STR_ENGINE_HEAVY_FREIGHT);
}
ENGINE_RUNNING_COST_SWITCH(et40, ET40)
DIESEL_ELECTRIC_2_ENGINE_POWER_SWITCH(et40, KW_TO_HP(4080))
ENGINE_VISUAL_EFFECT_SWITCH(et40)

/* Define the actual train */
item(FEAT_TRAINS, item_et40) {
    property {
        /* common properties */
        name:                           string(STR_ET40_NAME);
        climates_available:             PKP_CLIMATES; 
        introduction_date:              date(1975, 1, 1);
        vehicle_life:                   30;
        reliability_decay:              30;     // 20;
        CARGO_LOCOMOTIVES_DEFINITION
        loading_speed:                  5;
        cost_factor:                    PURCHASE_COST_ET40;
        running_cost_factor:            RUNNING_COST_ET40;
        /* train properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          100 km/h;
        misc_flags:                     bitmask();
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_CARGO;
        power:                          4080 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        ENGINE_CAPACITY_PROPERTY
        weight:                         164 ton;
        ai_engine_rank:                 0; // not intended to be used by the ai
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        extra_power_per_wagon:          0 kW;
        tractive_effort_coefficient:    0.280;
        air_drag_coefficient:           0.07;
        length:                         8;
        extra_weight_per_wagon:         0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_DEFAULT, 0, DISABLE_WAGON_POWER);
        bitmask_vehicle_info:           0;
    }

if(param_keep_old_vehs) {
    property {
        model_life:                     VEHICLE_NEVER_EXPIRES;
    }
} else {
    property {
        model_life:                     40;
        retire_early:                   2;
    }
}

    graphics {
        purchase:                   sset_et40_purchase;
        additional_text:            switch_et40_add_text;
        default:                    switch_et40_graphics;
        articulated_part:           switch_et40_articulated;
        length:                     switch_et40_length;
        visual_effect_and_powered:  switch_et40_visual_effect;
        can_attach_wagon:           switch_normal_can_attach;
        start_stop:                 switch_normal_start_stop;
        ENGINE_CAPACITY_CALLBACKS
        ENGINE_RUNNING_COST_CALLBACKS(et40)
        ENGINE_POWER_CALLBACKS(et40)
    }
}
